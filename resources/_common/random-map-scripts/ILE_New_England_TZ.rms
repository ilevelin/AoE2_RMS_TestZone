/* === New England (v1.6) ===
MADE BY ILEVELIN

Supported players/teams configuration:
	Any 2 team configuration with a limit of 4 players in a single team
	
Supported map sizes:
	2p
	3p
	4p
	6p
	8p
	
Supported game modes:
	ANY except Empire Wars

NOTE: 
	Playing with any non-supported player configuration, map size and/or game mode may place
	the players in unbalanced positions, bug the terrain generation or even not load. 
	Support to some other configurations MAY be introduced in the future.

Map reference: AoE3's New England
	https://vignette.wikia.nocookie.net/ageofempires/images/8/81/New_england_mini_map.png/revision/latest?cb=20170719024752

Scripting references:
	https://docs.aoe2map.net/ 
	http://userpatch.aiscripters.net/reference.html# 
	https://docs.google.com/document/d/1eXRO5ZoSwrVn_rwyVx4k3FhAmqBwRVVzs95OPyIOrd0/edit
	https://www.forgottenempires.net/age-of-empires-ii-definitive-edition/rms-features
	
Changelog: [ +:Added | -:Removed | *:Adjusted | !:Fixed ]
	( v1.6 ) 2022/11/06
		+ Now a new batch of ibex appear in the middle part of each lake for the pockets to enjoy
		* Now forests appear slightly further away from players
		! Fixed a critical bug that would leave some players (specially on the northen part) without gold, stone or berries
	( v1.5 ) 2022/11/04
		- Reduced ammount of ibex on lake sides on bigger team sizes from 2x to 1+x (x being the amount of players) on each side of both central lakes.
		! Fixed sometimes extra golds would fail to spawn for one or both teams, ending up in an unbalanced state where one of the teams had more gold on it's side
	( v1.4 ) 2022/06/10
		* Deer now spawn in the corners of the central lakes instead of having a specified amount close to each player. There is 4 spawn zones (one in each corner of each lake) where 2 deer per player will spawn
		* Now rhinos will allways appear one on the front and the other on the back of the base, being the backside allways the closest to the player
	( v1.3 ) 2022/06/03
		+ Increased the number of gold veins in the island from 8 to 10
		+ Increased the number of stone veins in the island from 4 to 6
		+ Increased the hill amount in the main terrain and increased the height limit from 1 to 2
		+ Removed the gold delta in main golds, increasing the main base available gold by 2400
		+ Added elevations in the gold and stone zones
		- Reduced neutral stone spawned
		- Reduced neutral gold spawned
		* Removed the stone delta in main stones, but removed one of the stones in each group
		* Adjusted the neutral gold generation to better balance how close each gold spawn to each team
		* Changed both neutral golds and stones to scale by player number instead of map size
		* Improved the spreading of relics in the central zone
		* Now the extra initial cows spawn spreaded in 3 groups instead of all 3 in one group
	( v1.2 ) 2020/07/22
		+ Added support for any 2 asymetrical teams configuration (to a maximum of 4 players in a team)
		+ Added 2 stone mines to the main stones, now divided in 2 groups
		+ Slightly increased gold zone size in bigger map sizes
		- Reduced the central stone amount by 50 per mine, for a total of 300 per group
		- Reduced the amount of relics in the center of the map from 2x to 2+x (x being the amount of players)
		* Readjusted elevation generation
		! Fixed winter terrain chance, going from 99% to the intended 35%
		! Fixed team position allways placing the players from south to north
	( v1.1 ) 2020/07/17
		- Slightly reduced fish amount
	( v1.0 ) 2020/06/26
		* Made the main golds appear in diferent sides of the base
	( v0.6 ) 2020/06/17
		+ Added support to DM and Regicide
	( v0.5 ) 2020/06/15
		- Reduced amount of resources on main golds
	( v0.4 ) 2020/06/13
		+ Added forests to the northen gold zone
		+ Added a mask over the central stone zone for better aesthetics
		+ Added a stone mine to the sourthen island
		- Removed one of the gold mines to the sourthen island
		- Reduced deep fish amount
		- Reduced the chance of getting the winter variation
		- Greatly reduced stone and gold amount overall
		* Slightly reduced size of the central lakes
		* Improved the distribution of stone mines in the central zone
		* Improved the distribution of gold mines in the northen zone
		* Readjusted forest zones to be greater in amount but smaller in size
	( v0.3 ) 2020/06/09
		+ Greatly increased shore fish amount
		+ Added winter terrain variation
	( v0.2 ) 2020/06/18
		! Fixed team position option not working.
	( v0.1 ) 2020/05/06
		+ Hello World!
*/

/* *** DEBUG CONFIGS *** */
/* #define VAR_DEBUG_GAMEMODE_RM */
/* #define VAR_DEBUG_GAMEMODE_EW */
/* #define VAR_DEBUG_GAMEMODE_DM */

/* #define VAR_DEBUG_GAMEMODE_REG */

/* #define VAR_DEBUG_TEAMS_POSITION1 */
/* #define VAR_DEBUG_TEAMS_POSITION2 */

/* #define VAR_DEBUG_TEAM1_ORIENTATION1 */
/* #define VAR_DEBUG_TEAM2_ORIENTATION1 */
/* #define VAR_DEBUG_TEAM1_ORIENTATION2 */
/* #define VAR_DEBUG_TEAM2_ORIENTATION2 */

/* #define VAR_DEBUG_SEASON_NORMAL */
/* #define VAR_DEBUG_SEASON_WINTER */

/* SEASON SELECTOR */
if VAR_DEBUG_SEASON_NORMAL
	#define VAR_SEASON_NORMAL
elseif VAR_DEBUG_SEASON_WINTER
	#define VAR_SEASON_WINTER
else
	start_random
	percent_chance 65 /* === Normal-65 === */
		#define VAR_SEASON_NORMAL
	percent_chance 35 /* === Winter-35 === */
		#define VAR_SEASON_WINTER
	end_random
endif

/* TRERRAINS */
if VAR_SEASON_NORMAL
	#const VAR_TERRAIN_BASE 12 /* GRASS2 */
	#const VAR_TERRAIN_GOLDZONE 9 /* GRASS3 */
	#const VAR_TERRAIN_ISLAND 3 /* DIRT3 */
	#const VAR_TERRAIN_FOREST 19 /* PINE_FOREST */
	#const VAR_TERRAIN_STONEZONE 12 /* GRASS2 */
	#const VAR_OBJECT_STRAGGLERTREE 350 /* PINE_TREE */
elseif VAR_SEASON_WINTER
	#const VAR_TERRAIN_BASE 34 /* GRASS_SNOW */
	#const VAR_TERRAIN_GOLDZONE 32 /* SNOW */
	#const VAR_TERRAIN_ISLAND 33 /* DIRT_SNOW */
	#const VAR_TERRAIN_FOREST 21 /* SNOW_FOREST */
	#const VAR_TERRAIN_STONEZONE 32 /* SNOW */
	#const VAR_OBJECT_STRAGGLERTREE 413 /* SNOW_PINE_TREE */
endif

/* PLAYER POSITIONING VARS */
if VAR_DEBUG_TEAMS_POSITION1
elseif VAR_DEBUG_TEAMS_POSITION2
	#define VAR_TEAMPLACEMENT_TEAMSIDE
else
	start_random
	percent_chance 50
		#define VAR_TEAMPLACEMENT_TEAMSIDE
	end_random
endif


if VAR_DEBUG_TEAM1_ORIENTATION1
elseif VAR_DEBUG_TEAM1_ORIENTATION2
	#define VAR_TEAMPLACEMENT_TEAM1ORDER
else
	start_random
	percent_chance 50
		#define VAR_TEAMPLACEMENT_TEAM1ORDER
	end_random
endif

if VAR_DEBUG_TEAM2_ORIENTATION1
elseif VAR_DEBUG_TEAM2_ORIENTATION2
	#define VAR_TEAMPLACEMENT_TEAM2ORDER
else
	start_random
	percent_chance 50
		#define VAR_TEAMPLACEMENT_TEAM2ORDER
	end_random
endif

/* GAMEMODE DETECTION */
if VAR_DEBUG_GAMEMODE_RM
elseif VAR_DEBUG_GAMEMODE_EW
	#define VAR_GAMEMODE_EW
elseif VAR_DEBUG_GAMEMODE_DM
	#define VAR_GAMEMODE_DM
elseif EMPIRE_WARS
	#define VAR_GAMEMODE_EW
elseif DEATH_MATCH
	#define VAR_GAMEMODE_DM
endif

if VAR_DEBUG_GAMEMODE_REG
	#define VAR_GAMEMODE_REG
elseif REGICIDE
	#define VAR_GAMEMODE_REG
endif

<PLAYER_SETUP>
direct_placement
ai_info_map_type COASTAL 0 0 0

<LAND_GENERATION>

base_terrain VAR_TERRAIN_BASE

if 2_PLAYER_GAME
	create_land /* T1 */
	{
		terrain_type VAR_TERRAIN_BASE
		number_of_tiles 300
		if VAR_TEAMPLACEMENT_TEAMSIDE
			land_position 82 52
		else 
			land_position 18 52
		endif
		assign_to_player 1
	}
	create_land /* T2 */
	{
		terrain_type VAR_TERRAIN_BASE
		number_of_tiles 300
		if VAR_TEAMPLACEMENT_TEAMSIDE
			land_position 18 52
		else 
			land_position 82 52
		endif
		assign_to_player 2
	}
else
	if TEAM1_SIZE2
		if VAR_TEAMPLACEMENT_TEAM1ORDER
			create_land /* T1-B */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 82 55
				else 
					land_position 18 55
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-T */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 78 26
				else 
					land_position 22 26
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		else
			create_land /* T1-T */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 78 26
				else 
					land_position 22 26
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-B */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 82 55
				else 
					land_position 18 55
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		endif
	elseif TEAM1_SIZE3
		if VAR_TEAMPLACEMENT_TEAM1ORDER
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 77 58
				else 
					land_position 23 58
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-P */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 88 38
				else 
					land_position 12 38
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 76 20
				else 
					land_position 24 20
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		else
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 76 20
				else 
					land_position 24 20
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-P */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 88 38
				else 
					land_position 12 38
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 77 58
				else 
					land_position 23 58
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		endif
	elseif TEAM1_SIZE4
		if VAR_TEAMPLACEMENT_TEAM1ORDER
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 73 60
				else 
					land_position 27 60
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 92 50
				else 
					land_position 8 50
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 90 30
				else 
					land_position 10 30
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 73 18
				else 
					land_position 27 18
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		else
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 73 18
				else 
					land_position 27 18
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 90 30
				else 
					land_position 10 30
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 92 50
				else 
					land_position 8 50
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 73 60
				else 
					land_position 27 60
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		endif
	else
		create_land /* T1 */
		{
			terrain_type VAR_TERRAIN_BASE
			number_of_tiles 300
			if VAR_TEAMPLACEMENT_TEAMSIDE
				land_position 82 52
			else 
				land_position 18 52
			endif
			assign_to AT_TEAM 0 0 0
		}
	endif

	if TEAM2_SIZE2
		if VAR_TEAMPLACEMENT_TEAM2ORDER
			create_land /* T2-B */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 18 55
				else 
					land_position 82 55
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-T */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 22 26
				else 
					land_position 78 26
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		else
			create_land /* T2-T */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 22 26
				else 
					land_position 78 26
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-B */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 18 55
				else 
					land_position 82 55
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		endif
	elseif TEAM2_SIZE3
		if VAR_TEAMPLACEMENT_TEAM2ORDER
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 23 58
				else 
					land_position 77 58
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-P */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 12 38
				else
					land_position 88 38
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 24 20
				else 
					land_position 76 20
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		else
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 24 20
				else 
					land_position 76 20
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-P */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 12 38
				else
					land_position 88 38
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 23 58
				else 
					land_position 77 58
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		endif
	elseif TEAM2_SIZE4
		if VAR_TEAMPLACEMENT_TEAM2ORDER
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 27 60
				else 
					land_position 73 60
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 8 50
				else
					land_position 92 50
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 10 30
				else 
					land_position 90 30
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 27 18
				else 
					land_position 73 18
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		else
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 27 18
				else 
					land_position 73 18
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 10 30
				else 
					land_position 90 30
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 8 50
				else
					land_position 92 50
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 27 60
				else 
					land_position 73 60
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		endif
	else
		create_land /* T2 */
		{
			terrain_type VAR_TERRAIN_BASE
			number_of_tiles 300
			if VAR_TEAMPLACEMENT_TEAMSIDE
				land_position 18 52
			else 
				land_position 82 52
			endif
			assign_to AT_TEAM 0 0 0
		}
	endif
endif

/* SMALL LAKES */
create_land /* Left Lake */
{
	terrain_type WATER
	land_percent 15
	border_fuzziness 15
	
	if TINY_MAP
		left_border 39
		right_border 60
		top_border 26
		bottom_border 50
	elseif SMALL_MAP
		left_border 39
		right_border 59
		top_border 25
		bottom_border 49
	elseif MEDIUM_MAP
		left_border 39
		right_border 58
		top_border 24
		bottom_border 48
	elseif LARGE_MAP
		left_border 39
		right_border 57
		top_border 23
		bottom_border 47
	else
		left_border 39
		right_border 56
		top_border 22
		bottom_border 46
	endif
	
}
create_land /* Right Lake */
{
	terrain_type WATER
	land_percent 15
	border_fuzziness 15
	
	if TINY_MAP
		left_border 60
		right_border 39
		top_border 26
		bottom_border 50
	elseif SMALL_MAP
		left_border 59
		right_border 39
		top_border 25
		bottom_border 49
	elseif MEDIUM_MAP
		left_border 58
		right_border 39
		top_border 24
		bottom_border 48
	elseif LARGE_MAP
		left_border 57
		right_border 39
		top_border 23
		bottom_border 47
	else
		left_border 56
		right_border 39
		top_border 22
		bottom_border 46
	endif
}

/* BOTTOM WATER */
create_land /* Water */
{
	terrain_type DLC_WATER5
	land_percent 100
	border_fuzziness 15
	land_id 100
	
	left_border 0
	right_border 0
	top_border 72
	bottom_border 0
}
create_land /* Gold Island */
{
	terrain_type VAR_TERRAIN_ISLAND
	land_percent 50
	border_fuzziness 10
	land_position 50 85
	
	left_border 45
	right_border 45
	top_border 84
	bottom_border 14
}

/* STONE ZONE */
create_land
{
	terrain_type DLC_CRACKED
	
	left_border 46
	right_border 46
	top_border 13
	bottom_border 32
}

/* GOLD ZONE */
create_land
{
	terrain_type VAR_TERRAIN_GOLDZONE
	land_id 50
	
	left_border 0
	right_border 0
	top_border 0
	if TINY_MAP
		bottom_border 92
	elseif SMALL_MAP
		bottom_border 93
	elseif MEDIUM_MAP
		bottom_border 93
	elseif LARGE_MAP
		bottom_border 94
	else
		bottom_border 94
	endif
}

<ELEVATION_GENERATION>
create_elevation 2 /* Main Land */
{
	base_terrain VAR_TERRAIN_BASE
	if TINY_MAP
		number_of_tiles 2048
		number_of_clumps 12
	elseif SMALL_MAP
		number_of_tiles 4096
		number_of_clumps 24
	elseif MEDIUM_MAP
		number_of_tiles 6144
		number_of_clumps 36
	elseif LARGE_MAP
		number_of_tiles 7168
		number_of_clumps 8192
	else
		number_of_tiles 10240
		number_of_clumps 60
	endif
	enable_balanced_elevation
}
create_elevation 1 /* Gold Terrain */
{
	base_terrain VAR_TERRAIN_GOLDZONE
	if TINY_MAP
		number_of_tiles 500
		number_of_clumps 10
	elseif SMALL_MAP
		number_of_tiles 1000
		number_of_clumps 20
	elseif MEDIUM_MAP
		number_of_tiles 1500
		number_of_clumps 30
	elseif LARGE_MAP
		number_of_tiles 2000
		number_of_clumps 40
	else
		number_of_tiles 2500
		number_of_clumps 50
	endif
	enable_balanced_elevation
}
create_elevation 1 /* Stone Terrain */
{
	base_terrain DLC_CRACKED
	if TINY_MAP
		number_of_tiles 200
		number_of_clumps 10
	elseif SMALL_MAP
		number_of_tiles 400
		number_of_clumps 20
	elseif MEDIUM_MAP
		number_of_tiles 600
		number_of_clumps 30
	elseif LARGE_MAP
		number_of_tiles 800
		number_of_clumps 40
	else
		number_of_tiles 1000
		number_of_clumps 50
	endif
	enable_balanced_elevation
}

create_elevation 2 /* Gold Island */
{
	base_terrain VAR_TERRAIN_ISLAND
	number_of_tiles 1000
	number_of_clumps 1
	set_scale_by_size
	enable_balanced_elevation
}

<TERRAIN_GENERATION>
create_terrain DEEP_WATER /* For the fishing zone */
{
	base_terrain DLC_WATER5
	land_percent 100
	number_of_clumps 100
	spacing_to_other_terrain_types 2
	clumping_factor 5
}
create_terrain VAR_TERRAIN_FOREST /* Wood please */
{
	base_terrain VAR_TERRAIN_BASE
	land_percent 7
	number_of_clumps 7
	set_avoid_player_start_areas 20
	clumping_factor 30
	spacing_to_other_terrain_types 5
	set_scale_by_size
	set_scale_by_groups
}
create_terrain VAR_TERRAIN_FOREST /* North golds */
{
	base_terrain VAR_TERRAIN_GOLDZONE
	number_of_tiles 80
	set_avoid_player_start_areas
	clumping_factor 30
	set_scale_by_size
	
	if TINY_MAP
		number_of_clumps 6
	elseif SMALL_MAP
		number_of_clumps 8
	elseif MEDIUM_MAP
		number_of_clumps 10
	elseif LARGE_MAP
		number_of_clumps 12
	else
		number_of_clumps 14
	endif
}
create_terrain VAR_TERRAIN_STONEZONE /* Stone zone mask */
{
	base_terrain DLC_CRACKED
	land_percent 100
	terrain_mask 1
}

<OBJECTS_GENERATION>

/* Player Things */
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object SCOUT
{
	number_of_objects 1
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}
create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 6
}

if VAR_GAMEMODE_DM
else
	/* Cows */
	create_object DLC_COW /* Close ones */
	{
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 8
		find_closest
		number_of_objects 3
		set_gaia_object_only
		set_tight_grouping
	}
	create_object DLC_COW /* Further ones */
	{
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		number_of_groups 3
		temp_min_distance_group_placement 15
		set_gaia_object_only
		set_tight_grouping
	}

	/* Boars */
	create_object DLC_RHINO
	{
		set_place_for_every_player
		min_distance_to_players 13
		max_distance_to_players 13
		number_of_groups 1
		set_gaia_object_only
		avoid_forest_zone 1
		find_closest_to_map_edge
	}
	create_object DLC_RHINO
	{
		set_place_for_every_player
		min_distance_to_players 18
		max_distance_to_players 18
		number_of_groups 1
		set_gaia_object_only
		avoid_forest_zone 1
		find_closest_to_map_center
	}

	/* Bushes */
	create_object FORAGE_BUSH
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 12
		number_of_objects 6
		set_gaia_object_only
		set_tight_grouping
		avoid_forest_zone 2
		actor_area 1
		actor_area_radius 5
	}

	/* Player Stragglers */
	create_object VAR_OBJECT_STRAGGLERTREE
	{
		number_of_groups 3
		set_place_for_every_player
		min_distance_to_players 4
		max_distance_to_players 6
		set_gaia_object_only
		terrain_to_place_on VAR_TERRAIN_BASE
		temp_min_distance_group_placement 7
	}
endif

/* Main Golds */
create_object GOLD
{
	set_place_for_every_player
	min_distance_to_players 13
	max_distance_to_players 15
	number_of_objects 3
	number_of_groups 2
	temp_min_distance_group_placement 18
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 2
	actor_area_radius 5
	avoid_actor_area 1
}

/* Main Stone */
create_object STONE
{
	set_place_for_every_player
	min_distance_to_players 13
	max_distance_to_players 15
	number_of_objects 2
	number_of_groups 2
	temp_min_distance_group_placement 18
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 3
	actor_area_radius 5
	avoid_actor_area 1
	avoid_actor_area 2
}

/* Fish */
create_object GREAT_FISH_MARLIN /* Deep ones */
{
	number_of_groups 256
	max_distance_to_other_zones 7
	terrain_to_place_on DEEP_WATER
	
	if TINY_MAP
		temp_min_distance_group_placement 6
	elseif SMALL_MAP
		temp_min_distance_group_placement 7
	elseif MEDIUM_MAP
		temp_min_distance_group_placement 8
	elseif LARGE_MAP
		temp_min_distance_group_placement 9
	else
		temp_min_distance_group_placement 10
	endif
}
create_object SHORE_FISH /* Shore ones */
{
	number_of_groups 256
	terrain_to_place_on DLC_WATER5
	temp_min_distance_group_placement 6
}

/* Extra Stones */
create_object STONE /* Center one */
{
	min_distance_to_players 25
	number_of_objects 4
	number_of_groups 3
	temp_min_distance_group_placement 8
	set_tight_grouping
	terrain_to_place_on DLC_CRACKED
	set_scaling_to_player_number
	max_distance_to_other_zones 6
}
create_object STONE /* Island one */
{
	min_distance_to_players 25
	number_of_objects 6
	temp_min_distance_group_placement 4
	set_tight_grouping
	terrain_to_place_on VAR_TERRAIN_ISLAND
	group_placement_radius 2
	max_distance_to_other_zones 1
	actor_area 10
	actor_area_radius 5
	avoid_all_actor_areas
}

/* Relics */
create_object RELIC /* Center ones / one per player */
{
	number_of_groups 1
	temp_min_distance_group_placement 20
	terrain_to_place_on DLC_CRACKED
	set_scaling_to_player_number
	actor_area 10
	actor_area_radius 20
}
create_object RELIC /* Center ones / base 2 */
{
	number_of_groups 2
	temp_min_distance_group_placement 20
	terrain_to_place_on DLC_CRACKED
	actor_area 11
	actor_area_radius 20
	avoid_actor_area 10
}
create_object RELIC /* Gold island one */
{
	temp_min_distance_group_placement 10
	terrain_to_place_on VAR_TERRAIN_ISLAND
}

/* Extra Golds */
if TINY_MAP /* Actor areas to restrict the zone where the golds can spawn */
	create_actor_area 0 0 200 50
	create_actor_area 120 0 201 50
elseif SMALL_MAP
	create_actor_area 0 0 200 65
	create_actor_area 144 0 201 65
elseif MEDIUM_MAP
	create_actor_area 0 0 200 75
	create_actor_area 168 0 201 75
elseif LARGE_MAP
	create_actor_area 0 0 200 90
	create_actor_area 200 0 201 90
elseif HUGE_MAP
	create_actor_area 0 0 200 100
	create_actor_area 220 0 201 100
elseif GIGANTIC_MAP
	create_actor_area 0 0 200 110
	create_actor_area 240 0 201 110
elseif LUDIKRIS_MAP
	create_actor_area 0 0 200 220
	create_actor_area 480 0 201 220
endif

create_object GOLD /* Northen ones, left team */
{
	force_placement
	group_placement_radius 2
	number_of_objects 8
	if 1_PLAYER_GAME
		number_of_groups 2
	elseif 2_PLAYER_GAME
		number_of_groups 3
	elseif 3_PLAYER_GAME
		number_of_groups 5
	elseif 4_PLAYER_GAME
		number_of_groups 6
	elseif 5_PLAYER_GAME
		number_of_groups 8
	elseif 6_PLAYER_GAME
		number_of_groups 9
	elseif 7_PLAYER_GAME
		number_of_groups 11
	elseif 8_PLAYER_GAME
		number_of_groups 12
	endif
	temp_min_distance_group_placement 9
	set_tight_grouping
	set_place_for_every_player
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_GOLDZONE
	actor_area_to_place_in 200
	avoid_forest_zone 1
}
create_object GOLD /* Northen ones, right team */
{
	force_placement
	group_placement_radius 2
	number_of_objects 8
	if 1_PLAYER_GAME
		number_of_groups 2
	elseif 2_PLAYER_GAME
		number_of_groups 3
	elseif 3_PLAYER_GAME
		number_of_groups 5
	elseif 4_PLAYER_GAME
		number_of_groups 6
	elseif 5_PLAYER_GAME
		number_of_groups 8
	elseif 6_PLAYER_GAME
		number_of_groups 9
	elseif 7_PLAYER_GAME
		number_of_groups 11
	elseif 8_PLAYER_GAME
		number_of_groups 12
	endif
	temp_min_distance_group_placement 9
	set_tight_grouping
	set_place_for_every_player
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_GOLDZONE
	actor_area_to_place_in 201
	avoid_forest_zone 1
}
create_object GOLD /* Island one */
{
	min_distance_to_players 25
	number_of_objects 10
	temp_min_distance_group_placement 4
	set_tight_grouping
	terrain_to_place_on VAR_TERRAIN_ISLAND
	group_placement_radius 2
	max_distance_to_other_zones 1
}

/* Deers */
if TINY_MAP /* Actor areas to indicate where deer can spawn */
	create_actor_area 46 31 300 4
	create_actor_area 74 31 301 4
	create_actor_area 46 60 302 4
	create_actor_area 74 60 303 4
	create_actor_area 42 45 304 4
	create_actor_area 78 45 305 4
elseif SMALL_MAP
	create_actor_area 55 36 300 5
	create_actor_area 89 36 301 5
	create_actor_area 55 73 302 5
	create_actor_area 89 73 303 5
	create_actor_area 53 55 304 5
	create_actor_area 91 55 305 5
elseif MEDIUM_MAP
	create_actor_area 63 40 300 5
	create_actor_area 105 40 301 5
	create_actor_area 63 87 302 5
	create_actor_area 105 87 303 5
	create_actor_area 60 63 304 5
	create_actor_area 105 63 305 5
elseif LARGE_MAP
	create_actor_area 78 46 300 6
	create_actor_area 122 46 301 6
	create_actor_area 78 106 302 6
	create_actor_area 122 106 303 6
	create_actor_area 76 76 304 8
	create_actor_area 124 76 305 8
elseif HUGE_MAP
	create_actor_area 86 48 300 6
	create_actor_area 134 48 301 6
	create_actor_area 86 119 302 6
	create_actor_area 134 119 303 6
	create_actor_area 85 83 304 10
	create_actor_area 135 83 305 10
elseif GIGANTIC_MAP
	create_actor_area 96 53 300 7
	create_actor_area 144 53 301 7
	create_actor_area 96 130 302 7
	create_actor_area 144 130 303 7
	create_actor_area 94 93 304 12
	create_actor_area 146 93 305 12
elseif LUDIKRIS_MAP
	create_actor_area 185 106 300 7
	create_actor_area 295 106 301 7
	create_actor_area 185 259 302 7
	create_actor_area 295 259 303 7
	create_actor_area 185 180 304 15
	create_actor_area 295 180 305 15
endif

create_object DLC_IBEX
{
	if 1_PLAYER_GAME
		number_of_groups 2
	elseif 2_PLAYER_GAME
		number_of_groups 3
	elseif 3_PLAYER_GAME
		number_of_groups 4
	elseif 4_PLAYER_GAME
		number_of_groups 5
	elseif 5_PLAYER_GAME
		number_of_groups 6
	elseif 6_PLAYER_GAME
		number_of_groups 7
	elseif 7_PLAYER_GAME
		number_of_groups 8
	elseif 8_PLAYER_GAME
		number_of_groups 9
	endif
	actor_area_to_place_in 300
	set_place_for_every_player
	set_gaia_object_only
}
create_object DLC_IBEX
{
	if 1_PLAYER_GAME
		number_of_groups 2
	elseif 2_PLAYER_GAME
		number_of_groups 3
	elseif 3_PLAYER_GAME
		number_of_groups 4
	elseif 4_PLAYER_GAME
		number_of_groups 5
	elseif 5_PLAYER_GAME
		number_of_groups 6
	elseif 6_PLAYER_GAME
		number_of_groups 7
	elseif 7_PLAYER_GAME
		number_of_groups 8
	elseif 8_PLAYER_GAME
		number_of_groups 9
	endif
	actor_area_to_place_in 301
	set_place_for_every_player
	set_gaia_object_only
}
create_object DLC_IBEX
{
	if 1_PLAYER_GAME
		number_of_groups 2
	elseif 2_PLAYER_GAME
		number_of_groups 3
	elseif 3_PLAYER_GAME
		number_of_groups 4
	elseif 4_PLAYER_GAME
		number_of_groups 5
	elseif 5_PLAYER_GAME
		number_of_groups 6
	elseif 6_PLAYER_GAME
		number_of_groups 7
	elseif 7_PLAYER_GAME
		number_of_groups 8
	elseif 8_PLAYER_GAME
		number_of_groups 9
	endif
	actor_area_to_place_in 302
	set_place_for_every_player
	set_gaia_object_only
}
create_object DLC_IBEX
{
	if 1_PLAYER_GAME
		number_of_groups 2
	elseif 2_PLAYER_GAME
		number_of_groups 3
	elseif 3_PLAYER_GAME
		number_of_groups 4
	elseif 4_PLAYER_GAME
		number_of_groups 5
	elseif 5_PLAYER_GAME
		number_of_groups 6
	elseif 6_PLAYER_GAME
		number_of_groups 7
	elseif 7_PLAYER_GAME
		number_of_groups 8
	elseif 8_PLAYER_GAME
		number_of_groups 9
	endif
	actor_area_to_place_in 303
	set_place_for_every_player
	set_gaia_object_only
}
create_object DLC_IBEX
{
	if 1_PLAYER_GAME
		number_of_groups 2
	elseif 2_PLAYER_GAME
		number_of_groups 3
	elseif 3_PLAYER_GAME
		number_of_groups 4
	elseif 4_PLAYER_GAME
		number_of_groups 5
	elseif 5_PLAYER_GAME
		number_of_groups 6
	elseif 6_PLAYER_GAME
		number_of_groups 7
	elseif 7_PLAYER_GAME
		number_of_groups 8
	elseif 8_PLAYER_GAME
		number_of_groups 9
	endif
	actor_area_to_place_in 304
	set_place_for_every_player
	set_gaia_object_only
}
create_object DLC_IBEX
{
	if 1_PLAYER_GAME
		number_of_groups 2
	elseif 2_PLAYER_GAME
		number_of_groups 3
	elseif 3_PLAYER_GAME
		number_of_groups 4
	elseif 4_PLAYER_GAME
		number_of_groups 5
	elseif 5_PLAYER_GAME
		number_of_groups 6
	elseif 6_PLAYER_GAME
		number_of_groups 7
	elseif 7_PLAYER_GAME
		number_of_groups 8
	elseif 8_PLAYER_GAME
		number_of_groups 9
	endif
	actor_area_to_place_in 305
	set_place_for_every_player
	set_gaia_object_only
}


/* Extra Stragglers */
create_object VAR_OBJECT_STRAGGLERTREE /* Mainland ones */
{
	number_of_groups 500
	set_scale_by_groups
	min_distance_to_players 12
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_BASE
	temp_min_distance_group_placement 12
}
create_object VAR_OBJECT_STRAGGLERTREE /* Goldzone ones */
{
	number_of_groups 500
	set_scale_by_groups
	min_distance_to_players 12
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_GOLDZONE
	temp_min_distance_group_placement 10
	actor_area 21
	actor_area_radius 3
	avoid_all_actor_areas
}
create_object VAR_OBJECT_STRAGGLERTREE /* Gold island ones */
{
	number_of_groups 500
	set_scale_by_groups
	min_distance_to_players 12
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_ISLAND
	temp_min_distance_group_placement 12
}

/* King and castle for regicide */
if REGICIDE
	create_object KING
	{
		number_of_objects 1
		set_place_for_every_player
		min_distance_to_players 7
		max_distance_to_players 9
	}
	create_object CASTLE
	{
		number_of_objects 1
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		avoid_all_actor_areas
	}
endif
