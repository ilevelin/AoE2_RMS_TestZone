/* === Green Pit (v2.2) ===
MADE BY ILEVELIN

Supported players/teams configuration:
	ANY
	
Supported map sizes:
	2p
	3p
	4p
	6p
	8p
	
Supported game modes:
	ANY
	
NOTE: 
	Playing with any non-supported player configuration, map size and/or game mode may place
	the players in unbalanced positions, bug the terrain generation or even not load. 
	Support to some other configurations MAY be introduced in the future.

Scripting references:
	https://docs.aoe2map.net/ 
	http://userpatch.aiscripters.net/reference.html# 
	https://docs.google.com/document/d/1eXRO5ZoSwrVn_rwyVx4k3FhAmqBwRVVzs95OPyIOrd0/edit
	https://www.forgottenempires.net/age-of-empires-ii-definitive-edition/rms-features

Changelog: [ +:Added | -:Removed | *:Adjusted | !:Fixed ]
	( v2.2 ) 2022/09/??
		* Forced the "first" main gold of each player to spawn as close as possible to the player while evading forest zone
			> This change is to enhance the spawn of the gold workers in EW, impact to RM should be negligent to none
		! Fixed a rare bug where some lumberjacks would fail to spawn in EW 
	( v2.1 ) 2022/09/04
		+ Increased central shore fish by 5 per player
		+ Added support for Empire Wars
		! Fixed deer not spawning on the second forage bush group
	( v2.0 ) 2022/08/22
		* Completly reworked the central forest area:
			> Now it is a lake of mangroove shallow terrain, with a dense forest of mangroove and little sneaking paths that lead to a clear center where players can find coast fish and some bears guarding them
	( v1.0 ) 2022/06/10
		+ Now small tree clumps will appear in the lower zone
		+ Increased the amount of trees on the upper zone
		- Reduced central forest size
		* Now extra sheep will spawn closer to the players
		* Now the elephant ("extra boar") will appear slightly further away from the players
	( v0.2 ) 2021/05/18
		- Completly removed one of the main golds
		- Removed one vein from all gold groups
		* Pushed extra golds further away from players
		* Pushed extra elephan further away from players
	( v0.1 ) 2021/03/21
		+ Hello World!

*/
/* *** DEBUG CONFIGS *** */
/* #define VAR_DEBUG_FORCE_RM */
/* #define VAR_DEBUG_FORCE_EW */
/* #define VAR_DEBUG_FORCE_DM */

/* #define VAR_DEBUG_FORCE_REG */

/* TERRAINS */
#const VAR_TERRAIN_BASE 16 /* ROCK1 (Grass, Other) */

#const VAR_TERRAIN_UPPER 9 /* GRASS3 */
#const VAR_TERRAIN_UPPER_MASK 14 /* DESERT */
#const VAR_TERRAIN_UPPER_FOREST 49 /* DLC_BAOBABFOREST */

#const VAR_TERRAIN_LAKE 54 /* DLC_MANGROOVESHALLOW */
#const VAR_TERRAIN_LAKE_PLACEHOLDER 22 /* DEEP_WATER */
#const VAR_TERRAIN_LAKE_PLACEHOLDER2 23 /* MED_WATER */
#const VAR_TERRAIN_LAKE_MASK 0 /* GRASS */
#const VAR_TERRAIN_LAKE_FOREST 55 /* DLC_MANGROOVEFOREST */

#const VAR_TERRAIN_FOREST 19 /* PINE_FOREST */

#const VAR_TERRAIN_CLIFF 100 /* DLC_DRYGRASS */

/* OBJECTS */
#const VAR_OBJECT_STRAGGLERTREE 349 /* OAKTREE */

#const VAR_OBJECT_PLACEHOLDER_SMALL 600 /* Flag A */
effect_amount SET_ATTRIBUTE VAR_OBJECT_PLACEHOLDER_SMALL ATTR_HITPOINTS 0

#const VAR_OBJECT_PLACEHOLDER_BIG 1376 /* Torch B */
effect_amount SET_ATTRIBUTE VAR_OBJECT_PLACEHOLDER_BIG ATTR_HITPOINTS 0

#const VAR_OBJECT_DOCK_BLOCKER 1654
effect_percent GAIA_SET_ATTRIBUTE VAR_OBJECT_DOCK_BLOCKER ATTR_RADIUS_1 50
effect_percent GAIA_SET_ATTRIBUTE VAR_OBJECT_DOCK_BLOCKER ATTR_RADIUS_2 50

#const VAR_OBJECT_LAZY_MALE 1285 /* FE Flag */
effect_amount SET_ATTRIBUTE VAR_OBJECT_LAZY_MALE ATTR_DEAD_ID 83
effect_amount SET_ATTRIBUTE VAR_OBJECT_LAZY_MALE ATTR_HITPOINTS 0

#const VAR_OBJECT_LAZY_FEMALE 851 /* ES Flag */
effect_amount SET_ATTRIBUTE VAR_OBJECT_LAZY_FEMALE ATTR_DEAD_ID 293
effect_amount SET_ATTRIBUTE VAR_OBJECT_LAZY_FEMALE ATTR_HITPOINTS 0

/* GAMEMODE DETECTION */
if VAR_DEBUG_FORCE_RM
elseif VAR_DEBUG_FORCE_EW
	#define VAR_GAMEMODE_EW
elseif VAR_DEBUG_FORCE_DM
	#define VAR_GAMEMODE_DM
elseif EMPIRE_WARS
	#define VAR_GAMEMODE_EW
elseif DEATH_MATCH
	#define VAR_GAMEMODE_DM
endif

if VAR_DEBUG_FORCE_REG
	#define VAR_GAMEMODE_REG
elseif REGICIDE
	#define VAR_GAMEMODE_REG
endif

/*
<PLAYER_SETUP>
*/

<LAND_GENERATION>
base_terrain VAR_TERRAIN_CLIFF

create_land /* Lower zone */
{
	terrain_type VAR_TERRAIN_BASE
	land_percent 100
	
	left_border 13
	right_border 13
	top_border 13
	bottom_border 13
}

create_player_lands /* The protagonists */
{
	terrain_type VAR_TERRAIN_BASE
	number_of_tiles 1
	if TINY_MAP
		circle_radius 34
	elseif SMALL_MAP
		circle_radius 34
	elseif MEDIUM_MAP
		circle_radius 35
	elseif LARGE_MAP
		circle_radius 35
	else
		circle_radius 36
	endif
}

<ELEVATION_GENERATION>

create_elevation 2
{
	base_terrain VAR_TERRAIN_CLIFF
	number_of_tiles 9999
	number_of_clumps 1
	set_scale_by_size
	enable_balanced_elevation
}

create_elevation 1
{
	base_terrain VAR_TERRAIN_BASE
	number_of_tiles rnd(1000,1300)
	number_of_clumps rnd(30,60)
	set_scale_by_size
	enable_balanced_elevation
}

create_elevation 3
{
	base_terrain VAR_TERRAIN_CLIFF
	number_of_tiles rnd(1000,1300)
	number_of_clumps rnd(100,130)
	set_scale_by_size
	enable_balanced_elevation
}

<TERRAIN_GENERATION>

create_terrain VAR_TERRAIN_UPPER
{
	base_terrain VAR_TERRAIN_CLIFF
	land_percent 100
	height_limits 2 3
}

create_terrain VAR_TERRAIN_LAKE /* Central lake */
{
	base_terrain VAR_TERRAIN_BASE
	land_percent 100
	if TINY_MAP
		spacing_to_other_terrain_types 20
	elseif SMALL_MAP
		spacing_to_other_terrain_types 21
	elseif MEDIUM_MAP
		spacing_to_other_terrain_types 22
	elseif LARGE_MAP
		spacing_to_other_terrain_types 23
	else
		spacing_to_other_terrain_types 24
	endif
	set_avoid_player_start_areas 1
}

create_terrain VAR_TERRAIN_LAKE_PLACEHOLDER /* This will become the central green patch */
{
	base_terrain VAR_TERRAIN_LAKE
	clumping_factor 10
	if TINY_MAP
		spacing_to_other_terrain_types 10
		land_percent 2
	elseif SMALL_MAP
		spacing_to_other_terrain_types 15
		land_percent 2
	elseif MEDIUM_MAP
		spacing_to_other_terrain_types 20
		land_percent 2
	elseif LARGE_MAP
		spacing_to_other_terrain_types 25
		land_percent 2
	else
		spacing_to_other_terrain_types 30
		land_percent 2
	endif
}

/* Placing central forests one by one so they appear with little paths to the centre */
create_terrain VAR_TERRAIN_LAKE_FOREST 
{
	base_terrain VAR_TERRAIN_LAKE
	land_percent 2
	spacing_to_other_terrain_types 2
}

create_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
{
	base_terrain VAR_TERRAIN_LAKE
	land_percent 2
	spacing_to_other_terrain_types 2
	clumping_factor 25
}

create_terrain VAR_TERRAIN_LAKE_FOREST
{
	base_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
	land_percent 100
}

create_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
{
	base_terrain VAR_TERRAIN_LAKE
	land_percent 2
	spacing_to_other_terrain_types 2
	clumping_factor 25
}

create_terrain VAR_TERRAIN_LAKE_FOREST
{
	base_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
	land_percent 100
}

create_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
{
	base_terrain VAR_TERRAIN_LAKE
	land_percent 2
	spacing_to_other_terrain_types 2
	clumping_factor 25
}

create_terrain VAR_TERRAIN_LAKE_FOREST
{
	base_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
	land_percent 100
}

create_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
{
	base_terrain VAR_TERRAIN_LAKE
	land_percent 2
	spacing_to_other_terrain_types 2
	clumping_factor 25
}

create_terrain VAR_TERRAIN_LAKE_FOREST
{
	base_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
	land_percent 100
}

create_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
{
	base_terrain VAR_TERRAIN_LAKE
	land_percent 2
	spacing_to_other_terrain_types 2
	clumping_factor 25
}

create_terrain VAR_TERRAIN_LAKE_FOREST
{
	base_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
	land_percent 100
}

create_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
{
	base_terrain VAR_TERRAIN_LAKE
	land_percent 2
	spacing_to_other_terrain_types 2
	clumping_factor 25
}

create_terrain VAR_TERRAIN_LAKE_FOREST
{
	base_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
	land_percent 100
}

create_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
{
	base_terrain VAR_TERRAIN_LAKE
	land_percent 2
	spacing_to_other_terrain_types 2
	clumping_factor 25
}

create_terrain VAR_TERRAIN_LAKE_FOREST
{
	base_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
	land_percent 100
}

create_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
{
	base_terrain VAR_TERRAIN_LAKE
	land_percent 2
	spacing_to_other_terrain_types 2
	clumping_factor 25
}

create_terrain VAR_TERRAIN_LAKE_FOREST
{
	base_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
	land_percent 100
}

create_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
{
	base_terrain VAR_TERRAIN_LAKE
	land_percent 2
	spacing_to_other_terrain_types 2
	clumping_factor 25
}

create_terrain VAR_TERRAIN_LAKE_FOREST
{
	base_terrain VAR_TERRAIN_LAKE_PLACEHOLDER2
	land_percent 100
}

create_terrain VAR_TERRAIN_LAKE
{
	base_terrain VAR_TERRAIN_LAKE_PLACEHOLDER
	land_percent 100
}

create_terrain VAR_TERRAIN_LAKE_MASK /* Central green spot in the lake */
{
	base_terrain VAR_TERRAIN_LAKE
	land_percent 100
	terrain_mask 1
	spacing_to_other_terrain_types 4
}

create_terrain VAR_TERRAIN_UPPER_MASK  /* Upper zone terrain mask */
{
	base_terrain VAR_TERRAIN_UPPER
	land_percent 100
	terrain_mask 1
}

create_terrain VAR_TERRAIN_UPPER_FOREST /* Upper zone wood */
{
	base_terrain VAR_TERRAIN_UPPER
	land_percent 8
	number_of_clumps 48
	set_scaling_to_map_size
}

create_terrain VAR_TERRAIN_FOREST /* Extra wood on lower zone */
{
	base_terrain VAR_TERRAIN_BASE
	land_percent 2
	set_avoid_player_start_areas 30
	spacing_to_other_terrain_types 3
	number_of_clumps 6
	set_scale_by_groups
}

create_terrain SHALLOW
{
	base_terrain VAR_TERRAIN_BASE
	land_percent 5
	number_of_clumps 99
	terrain_mask 1
}

/*
<CONNECTION_GENERATION>
*/

<OBJECTS_GENERATION> 

/* Player Thingies */
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object SCOUT
{
	number_of_objects 1
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}

if VAR_GAMEMODE_DM
else
	/* Bushes and Deers */
	create_object FORAGE_BUSH
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 13
		number_of_objects 5
		set_gaia_object_only
		set_tight_grouping
		avoid_forest_zone 8
		actor_area 100
		actor_area_radius 1
		avoid_all_actor_areas
		terrain_to_place_on VAR_TERRAIN_BASE
	}
	create_object DLC_IBEX
	{
		set_place_for_every_player
		number_of_objects 3
		actor_area_to_place_in 100
		actor_area 101
		actor_area_radius 15
		set_tight_grouping
		set_gaia_object_only
	}
	
	create_object FORAGE_BUSH
	{
		set_place_for_every_player
		min_distance_to_players 20
		max_distance_to_players 22
		number_of_objects 5
		set_gaia_object_only
		set_tight_grouping
		avoid_forest_zone 8
		avoid_actor_area 101
		actor_area 102
		actor_area_radius 1
		terrain_to_place_on VAR_TERRAIN_BASE
	}
	create_object DLC_IBEX
	{
		set_place_for_every_player
		number_of_objects 3
		actor_area_to_place_in 102
		set_tight_grouping
		set_gaia_object_only
	}
endif
/* Central fish */
create_object VAR_OBJECT_PLACEHOLDER_SMALL
{
	number_of_groups 20
	second_object SHORE_FISH
	set_scaling_to_player_number
	terrain_to_place_on VAR_TERRAIN_LAKE
	layer_to_place_on VAR_TERRAIN_LAKE_MASK

	actor_area 9001
	actor_area_radius 10
}

/* Central bears */
create_object DLC_BEAR
{
	number_of_groups 2
	set_scaling_to_player_number
	terrain_to_place_on VAR_TERRAIN_LAKE
	layer_to_place_on VAR_TERRAIN_LAKE_MASK
	temp_min_distance_group_placement 5
}

/* Extra Foods */
create_object DLC_ZEBRA
{
	number_of_objects 1
	number_of_groups 7
	set_scaling_to_player_number
	avoid_forest_zone 2
	terrain_to_place_on VAR_TERRAIN_BASE
	min_distance_to_players 30
	temp_min_distance_group_placement 10
}

create_object FORAGE_BUSH
{
	number_of_objects 3
	number_of_groups 2
	set_tight_grouping
	set_scaling_to_player_number
	avoid_forest_zone 2
	terrain_to_place_on VAR_TERRAIN_BASE
	min_distance_to_players 30
	temp_min_distance_group_placement 12
}


/* Other Stragglers */
create_object VAR_OBJECT_STRAGGLERTREE
{
	number_of_groups 500
	set_scale_by_groups
	min_distance_to_players 20
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_BASE
	temp_min_distance_group_placement 5
	avoid_all_actor_areas
}

/* Main Golds */
create_object GOLD
{
	number_of_objects 4
	set_tight_grouping
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 15
	avoid_forest_zone 2
	find_closest
	terrain_to_place_on VAR_TERRAIN_UPPER
	if TINY_MAP
		max_distance_to_players 25
	elseif SMALL_MAP
		max_distance_to_players 27
	elseif MEDIUM_MAP
		max_distance_to_players 29
	elseif LARGE_MAP
		max_distance_to_players 31
	else
		max_distance_to_players 33
	endif
	temp_min_distance_group_placement 10

	actor_area 1300
	actor_area_radius 1
}
create_object VAR_OBJECT_PLACEHOLDER_SMALL
{
	set_place_for_every_player
	actor_area_to_place_in 1300
	actor_area 1301
	actor_area_radius 10
}

create_object GOLD
{
	number_of_objects 4
	set_tight_grouping
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 1
	terrain_to_place_on VAR_TERRAIN_UPPER
	if TINY_MAP
		max_distance_to_players 25
	elseif SMALL_MAP
		max_distance_to_players 27
	elseif MEDIUM_MAP
		max_distance_to_players 29
	elseif LARGE_MAP
		max_distance_to_players 31
	else
		max_distance_to_players 33
	endif
	avoid_actor_area 1301
}

/* Main Stones */
create_object STONE
{
	number_of_objects 3
	number_of_groups 2
	set_tight_grouping
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 1
	terrain_to_place_on VAR_TERRAIN_UPPER
	if TINY_MAP
		max_distance_to_players 25
	elseif SMALL_MAP
		max_distance_to_players 27
	elseif MEDIUM_MAP
		max_distance_to_players 29
	elseif LARGE_MAP
		max_distance_to_players 31
	else
		max_distance_to_players 33
	endif
	temp_min_distance_group_placement 15
	
	avoid_actor_area 1301

	actor_area 1400
	actor_area_radius 2
}

/* Extra Golds */
create_object GOLD
{
	number_of_objects 2
	number_of_groups 7
	set_tight_grouping
	set_scaling_to_map_size
	terrain_to_place_on VAR_TERRAIN_UPPER
	temp_min_distance_group_placement 15
	if TINY_MAP
		min_distance_to_players 32
	elseif SMALL_MAP
		min_distance_to_players 34
	elseif MEDIUM_MAP
		min_distance_to_players 36
	elseif LARGE_MAP
		min_distance_to_players 38
	else
		min_distance_to_players 40
	endif
}

/* Extra Stones */
create_object STONE
{
	number_of_objects 2
	number_of_groups 3
	set_tight_grouping
	set_scaling_to_map_size
	terrain_to_place_on VAR_TERRAIN_UPPER
	temp_min_distance_group_placement 30
	if TINY_MAP
		min_distance_to_players 30
	elseif SMALL_MAP
		min_distance_to_players 32
	elseif MEDIUM_MAP
		min_distance_to_players 34
	elseif LARGE_MAP
		min_distance_to_players 36
	else
		min_distance_to_players 38
	endif
	
	avoid_actor_area 1400
	avoid_actor_area 1301
}

/* Relics */
create_object RELIC
{
	terrain_to_place_on VAR_TERRAIN_UPPER
	min_distance_to_players 25
	number_of_objects 1
	number_of_groups 3
	avoid_forest_zone 3
	actor_area 60
	actor_area_radius 25
	temp_min_distance_group_placement 25
}

create_object RELIC
{
	terrain_to_place_on VAR_TERRAIN_UPPER
	min_distance_to_players 25
	number_of_objects 1
	number_of_groups 1
	set_scaling_to_player_number
	avoid_forest_zone 3
	avoid_actor_area 60
	temp_min_distance_group_placement 25
	max_distance_to_players 50
}

create_object VAR_OBJECT_DOCK_BLOCKER
{
	number_of_objects 2048
	set_gaia_object_only
	set_gaia_unconvertible
	set_scaling_to_map_size
	terrain_to_place_on BEACH
}

/* King and castle for regicide */
if VAR_GAMEMODE_REG
	create_object KING
	{
		number_of_objects 1
		set_place_for_every_player
		min_distance_to_players 7
		max_distance_to_players 9
	}
	create_object CASTLE
	{
		number_of_objects 1
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		avoid_all_actor_areas
	}
endif

if VAR_GAMEMODE_DM
elseif VAR_GAMEMODE_EW
	/* Farms */
	create_object FARM
	{
		second_object VILLAGER
		set_place_for_every_player
		number_of_objects 2
		find_closest
	}
	/* Mill and foragers */
	create_object MILL
	{
		set_place_for_every_player
		actor_area_to_place_in 100
		actor_area 1100
		actor_area_radius 0
		find_closest
		terrain_to_place_on VAR_TERRAIN_BASE
	}
	create_object VILLAGER
	{
		set_place_for_every_player
		actor_area_to_place_in 100
		avoid_actor_area 1100
		number_of_objects 4
		find_closest
		terrain_to_place_on VAR_TERRAIN_BASE
	}
	/* Sheep */
	create_object VAR_OBJECT_PLACEHOLDER_BIG /* Placeholders to force sheep spawning position */
	{
		set_place_for_every_player
		set_gaia_object_only
		number_of_objects 5
		min_distance_to_players 1
		max_distance_to_players 1
		find_closest

		actor_area 1110
		actor_area_radius 0
	}
	create_object VAR_OBJECT_PLACEHOLDER_BIG /* THE sheep */
	{
		second_object SHEEP
		set_place_for_every_player
		min_distance_to_players 1
		max_distance_to_players 1
		number_of_objects 1
		find_closest

		actor_area 1111
		actor_area_radius 1

		avoid_actor_area 1110
	}
	create_object VAR_OBJECT_PLACEHOLDER_BIG /* Placeholder to avoid villagers spawning on top of the sheep */
	{
		set_place_for_every_player
		min_distance_to_players 1
		actor_area_to_place_in 1111
		avoid_actor_area 1110
		find_closest

		actor_area 1900
		actor_area_radius 0
		
	}
	create_object VAR_OBJECT_PLACEHOLDER_BIG /* 3 villagers, males */
	{
		second_object VAR_OBJECT_LAZY_MALE
		set_place_for_every_player
		min_distance_to_players 1
		number_of_objects 3
		actor_area_to_place_in 1111
		avoid_actor_area 1900
		avoid_actor_area 1901
		find_closest

		actor_area 1901
		actor_area_radius 0
	}
	create_object VAR_OBJECT_PLACEHOLDER_BIG /* 3 villagers, females */
	{
		second_object VAR_OBJECT_LAZY_FEMALE
		set_place_for_every_player
		min_distance_to_players 1
		number_of_objects 3
		actor_area_to_place_in 1111
		avoid_actor_area 1900
		avoid_actor_area 1901
		find_closest

		actor_area 1901
		actor_area_radius 0
	}
	create_object SHEEP /* The remaining sheep */
	{
		set_place_for_every_player
		min_distance_to_players 5
		max_distance_to_players 5
		number_of_objects 4
		set_tight_grouping

		avoid_actor_area 1100
	}
	/* Wood */
	create_object VAR_OBJECT_PLACEHOLDER_BIG /* First Lumbercamp */
	{
		second_object LUMBER_CAMP
		set_place_for_every_player
		terrain_to_place_on VAR_TERRAIN_LAKE
		place_on_forest_zone
		avoid_actor_area 9001
		find_closest

		actor_area 1200
		actor_area_radius 3
	}
	create_object VILLAGER /* Villagers for first lumbercamp */
	{
		set_place_for_every_player
		place_on_forest_zone
		number_of_objects 4
		actor_area_to_place_in 1200
		avoid_actor_area 1210
		terrain_to_place_on VAR_TERRAIN_LAKE
		find_closest
		force_placement

		actor_area 1210
		actor_area_radius 7
	}

	create_object VAR_OBJECT_PLACEHOLDER_BIG /* Second Lumbercamp */
	{
		second_object LUMBER_CAMP
		set_place_for_every_player
		terrain_to_place_on VAR_TERRAIN_LAKE
		place_on_forest_zone
		avoid_actor_area 1200
		avoid_actor_area 1210
		avoid_actor_area 9001
		find_closest

		actor_area 1201
		actor_area_radius 3
	}
	create_object VILLAGER /* Villagers for second lumbercamp */
	{
		set_place_for_every_player
		place_on_forest_zone
		number_of_objects 4
		actor_area_to_place_in 1201
		avoid_actor_area 1210
		terrain_to_place_on VAR_TERRAIN_LAKE
		find_closest
		force_placement

		actor_area 1210
		actor_area_radius 7
	}

	create_object VAR_OBJECT_PLACEHOLDER_BIG /* Third Lumbercamp */
	{
		second_object LUMBER_CAMP
		set_place_for_every_player
		terrain_to_place_on VAR_TERRAIN_LAKE
		place_on_forest_zone
		avoid_actor_area 1200
		avoid_actor_area 1201
		avoid_actor_area 1210
		avoid_actor_area 9001
		find_closest

		actor_area 1202
		actor_area_radius 3
	}
	create_object VILLAGER /* Villagers for third lumbercamp */
	{
		set_place_for_every_player
		place_on_forest_zone
		number_of_objects 4
		actor_area_to_place_in 1202
		avoid_actor_area 1210
		terrain_to_place_on VAR_TERRAIN_LAKE
		find_closest
		force_placement

		actor_area 1210
		actor_area_radius 7
	}

	/* Gold */
	create_object MINING_CAMP
	{
		set_place_for_every_player
		actor_area_to_place_in 1300
		find_closest
		min_distance_to_map_edge 2
	}

	create_object VILLAGER
	{
		set_place_for_every_player
		actor_area_to_place_in 1300
		find_closest
		number_of_objects 4
		avoid_actor_area 1400
		avoid_forest_zone 1
	}

	/* Other buildings */
	create_object HOUSE
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 10
		number_of_objects 6
		find_closest_to_map_center
		avoid_actor_area 100
		avoid_actor_area 1210
	}
	create_object BARRACKS
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 10
		find_closest_to_map_center
		avoid_actor_area 100
		avoid_actor_area 1210
	}
	create_object BLACKSMITH
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 10
		find_closest_to_map_center
		avoid_actor_area 100
		avoid_actor_area 1210
	}
else
	create_object VILLAGER
	{
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 6
	}

	/* Sheeps */
	create_object SHEEP /* Close ones */
	{
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 8
		find_closest
		number_of_objects 4
		set_gaia_object_only
		set_tight_grouping
		terrain_to_place_on VAR_TERRAIN_BASE
	}
	create_object SHEEP /* Further ones */
	{
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		number_of_objects 2
		number_of_groups 2
		temp_min_distance_group_placement 10
		set_gaia_object_only
		set_tight_grouping
		terrain_to_place_on VAR_TERRAIN_BASE
	}

	/* Boars */
	create_object DLC_RHINO
	{
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 17
		number_of_groups 2
		set_gaia_object_only
		avoid_forest_zone 2
		temp_min_distance_group_placement 25
		terrain_to_place_on VAR_TERRAIN_BASE
	}	
	create_object DLC_ELEPHANT
	{
		set_place_for_every_player
		min_distance_to_players 28
		max_distance_to_players 30
		number_of_groups 1
		set_gaia_object_only
		avoid_forest_zone 2
		temp_min_distance_group_placement 25
		terrain_to_place_on VAR_TERRAIN_BASE
	}

	/* Player Stragglers */
	create_object VAR_OBJECT_STRAGGLERTREE
	{
		number_of_groups 6
		set_place_for_every_player
		min_distance_to_players 2
		max_distance_to_players 8
		set_gaia_object_only
		terrain_to_place_on VAR_TERRAIN_BASE
		temp_min_distance_group_placement 3
	}
endif