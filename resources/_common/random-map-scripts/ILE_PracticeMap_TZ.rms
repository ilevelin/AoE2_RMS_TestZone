/* === PRACTICE MAP (v1.1) ===
MADE BY
	ILEVELIN

Supported players/teams configuration:
	ONLY 1 PLAYER
	
Supported map sizes:
	ANY
	
Supported game modes:
	RANDOM MAP ONLY

NOTE: 
	Playing with any non-supported player configuration, map size and/or game mode may place
	the players in unbalanced positions, bug the terrain generation or even not load. 
	Support to some other configurations MAY be introduced in the future.

Scripting references:
	https://docs.aoe2map.net/ 
	
Changelog: [ +:Added | -:Removed | *:Adjusted | !:Fixed ]
	( v1.1 ) 2023/03/26
		* Changed most per-player objects to spawn in a circular raidus instead of a square one
	( v1.0 ) 2022/12/16
		+ Hello world!
*/

/* *** DEBUG CONFIGS *** */
/* #define VAR_DEBUG_GAMEMODE_RM */
/* #define VAR_DEBUG_GAMEMODE_EW */
/* #define VAR_DEBUG_GAMEMODE_DM */

/* #define VAR_DEBUG_GAMEMODE_REG */

/* TERRAINS */
#const VAR_TERRAIN_BASE 0 /* GRASS */
#const VAR_TERRAIN_FOREST 10 /* FOREST */

#const VAR_TERRAIN_PLACEHOLDER1 9 /* GRASS3 */
#const VAR_TERRAIN_PLACEHOLDER2 12 /* GRASS2 */

#const VAR_OBJECT_STRAGGLERTREE 349 /* OAKTREE */

#const OBJECT_DOCKBLOCKER 1654
effect_percent GAIA_SET_ATTRIBUTE OBJECT_DOCKBLOCKER ATTR_RADIUS_1 50
effect_percent GAIA_SET_ATTRIBUTE OBJECT_DOCKBLOCKER ATTR_RADIUS_2 50

<PLAYER_SETUP>
direct_placement

<LAND_GENERATION>
base_terrain VAR_TERRAIN_BASE

create_land
{
	terrain_type VAR_TERRAIN_PLACEHOLDER1
	land_position 50 50
	assign_to_player 1
	land_percent 100
	left_border 23
	right_border 23
	top_border 23
	bottom_border 23
}

/*
<ELEVATION_GENERATION>
*/
<TERRAIN_GENERATION>
create_terrain VAR_TERRAIN_FOREST
{
	base_terrain VAR_TERRAIN_PLACEHOLDER1
	land_percent 2
	spacing_to_other_terrain_types 5
	set_avoid_player_start_areas 17
	clumping_factor 25
}

create_terrain VAR_TERRAIN_PLACEHOLDER2
{
	base_terrain VAR_TERRAIN_PLACEHOLDER1
	land_percent 2
	spacing_to_other_terrain_types 5
	set_avoid_player_start_areas 17
	clumping_factor 25
}

create_terrain VAR_TERRAIN_FOREST
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	land_percent 100
	number_of_clumps 99999
}

create_terrain VAR_TERRAIN_PLACEHOLDER2
{
	base_terrain VAR_TERRAIN_PLACEHOLDER1
	land_percent 2
	spacing_to_other_terrain_types 5
	set_avoid_player_start_areas 17
	clumping_factor 25
}

create_terrain VAR_TERRAIN_FOREST
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	land_percent 100
	number_of_clumps 99999
}

create_terrain VAR_TERRAIN_FOREST
{
	base_terrain VAR_TERRAIN_BASE
	land_percent 7
	number_of_clumps 30
}

<CONNECTION_GENERATION>

<OBJECTS_GENERATION> 

/* Player Thingies */
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object SCOUT
{
	number_of_objects 1
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}
create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 6
}

create_object GOLD
{
	set_place_for_every_player
	min_distance_to_players 13
	max_distance_to_players 15
	number_of_objects 7
	temp_min_distance_group_placement 18
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 5
	actor_area 1
	actor_area_radius 4
	force_placement
	set_circular_placement
}

create_object STONE
{
	set_place_for_every_player
	min_distance_to_players 13
	max_distance_to_players 15
	number_of_objects 4
	temp_min_distance_group_placement 18
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 5
	avoid_actor_area 1
	actor_area 2
	actor_area_radius 3
	force_placement
	set_circular_placement
}

/* Sheeps */
create_object SHEEP /* Close ones */
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	find_closest
	number_of_objects 3
	set_gaia_object_only
	set_tight_grouping
	actor_area 999
	actor_area_radius 1
	set_circular_placement
}
create_object SHEEP /* Close ones */
{
	set_place_for_every_player
	find_closest
	set_tight_grouping
	actor_area_to_place_in 999
}
create_object SHEEP /* Further ones */
{
	set_place_for_every_player
	min_distance_to_players 15
	max_distance_to_players 20
	number_of_objects 2
	number_of_groups 2
	temp_min_distance_group_placement 10
	set_gaia_object_only
	set_tight_grouping
	set_circular_placement
}

/* Boars */
create_object BOAR
{
	set_place_for_every_player
	min_distance_to_players 13
	max_distance_to_players 13
	number_of_groups 1
	set_gaia_object_only
	avoid_forest_zone 1
	find_closest_to_map_edge
	set_circular_placement
}
create_object BOAR
{
	set_place_for_every_player
	min_distance_to_players 18
	max_distance_to_players 18
	number_of_groups 1
	set_gaia_object_only
	avoid_forest_zone 1
	find_closest_to_map_center
	set_circular_placement
}

/* Bushes */
create_object FORAGE_BUSH
{
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 12
	number_of_objects 6
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	avoid_all_actor_areas
	actor_area 3
	actor_area_radius 10
	set_circular_placement
}

/* Deers */
create_object DLC_IBEX
{
	set_place_for_every_player
	min_distance_to_players 13
	max_distance_to_players 17
	number_of_objects 3
	set_tight_grouping
	set_gaia_object_only
	avoid_actor_area 3
	avoid_forest_zone 3
	set_circular_placement
}

create_object STONE
{
	terrain_to_place_on VAR_TERRAIN_BASE
	number_of_groups 99
	number_of_objects 2
	set_tight_grouping
	temp_min_distance_group_placement 20
	min_distance_to_map_edge 5
	actor_area 10
	actor_area_radius 10
	avoid_forest_zone 5
}

create_object GOLD
{
	terrain_to_place_on VAR_TERRAIN_BASE
	number_of_groups 99
	number_of_objects 2
	set_tight_grouping
	temp_min_distance_group_placement 20
	min_distance_to_map_edge 5
	avoid_actor_area 10
	avoid_forest_zone 5
}

/* Player Stragglers */
create_object VAR_OBJECT_STRAGGLERTREE
{
	number_of_groups 5
	set_place_for_every_player
	min_distance_to_players 4
	max_distance_to_players 6
	set_gaia_object_only
	temp_min_distance_group_placement 7
}