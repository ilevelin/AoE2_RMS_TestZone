/* === Forest Pass (v1.2) ===
MADE BY ILEVELIN
	
Supported players/teams configuration:
	Any 2 team configuration with a limit of 4 players in a single team
	
Supported map sizes:
	2p
	3p
	4p
	6p
	8p
	
Supported game modes:
	ANY except Empire Wars
	
NOTE: 
	Playing with any non-supported player configuration, map size and/or game mode may place
	the players in unbalanced positions, bug the terrain generation or even not load. 
	Support to some other configurations MAY be introduced in the future.
	
Scripting references:
	https://docs.aoe2map.net/ 
	
Changelog: [ +:Added | -:Removed | *:Adjusted | !:Fixed ]
	( v1.2 ) Unpublished
		* Changed most per-player objects to spawn in a circular raidus instead of a square one
	( v1.1 ) 2022/12/16
		+ Made one of the main sheep automatically captured
	( v1.0 ) 2022/06/10
		+ Rescaled the amount of extra gold on the map, increasing it at the end from 12000 per player to 28000 per player
		  > Increased the amount of veins per player from 4 to 8
		  > Increased the amount of gold per vein from 3000 to 3750
		+ Rescaled the amount of extra stone on the map, increasing it at the end from 2000 per player to 3000 per player
		  > Increased the amount of veins per player from 2 to 6
		  > Reduced the amount of stone veins from 1000 to 500
		- Reduced player gold from 2500 per vein to 1000, bringing down the total gold on the player zones from 10000 to 4000
		- Reduced player stone from 500 per vein to 300, bringing down the total stone on the player zones from 2000 to 1200
		- Reduced relic amount from 2 per player to 1 per player plus 3
		* Changed the player spawn system to a direct_placement one
		  > This creates more balanced starting positions, but restricts the supported player/team configuration to only 2 teams and a maximum of 4 players per team
		* Now the forest lines only appear on the top and bottom of the map to make easier to maintain the change above
	( v0.4 ) 2020/06/26
		+ Added a second main stone to each player
		+ Doubled the amount of extra golds and stones
		- Reduced both main stones resource amount to half
		- Reduced all extra golds and stones resource amount to half
		* Made the main stones and golds appear in diferent sides of the base
		* Slightly augmented the minum separation between players
	( v0.3 ) 2020/06/17
		+ Added support to DM and Regicide
	( v0.2 ) 2020/06/15
		- Reduced amount of resources on main golds
		- Reduced amount of resources on main stones
	( v0.1 ) 2020/06/13
		+ Hello World!
 */
 
/* Which team goes where */
start_random
percent_chance 50
	#define VAR_TEAMPLACEMENT_TEAMSIDE
end_random

/* Team placement order */
start_random
percent_chance 50
	#define VAR_TEAMPLACEMENT_TEAM1ORDER
end_random

start_random
percent_chance 50
	#define VAR_TEAMPLACEMENT_TEAM2ORDER
end_random

/* Terrain flavour */
start_random 
percent_chance 37 /* Standard */
	#const VAR_TERRAIN_FOREST 10 /* FOREST */
	#const VAR_TERRAIN_BASE 60 /* DLC_JUNGLEGRASS */
	#const VAR_OBJECT_STRAGGLERTREE 349 /* OAK_FOREST_TREE */
percent_chance 37 /* Jungle */
	#const VAR_TERRAIN_FOREST 17 /* JUNGLE */
	#const VAR_TERRAIN_BASE 62 /* DLC_JUNGLELEAVES */
	#const VAR_OBJECT_STRAGGLERTREE 414 /* JUNGLE_TREE */
percent_chance 26 /* Winter */
	#const VAR_TERRAIN_FOREST 21 /* SNOW_FOREST */
	#const VAR_TERRAIN_BASE 34 /* GRASS_SNOW */
	#const VAR_OBJECT_STRAGGLERTREE 413 /* SNOW_FOREST */
end_random

<PLAYER_SETUP>
direct_placement

<LAND_GENERATION>
base_terrain VAR_TERRAIN_BASE

create_land /* South-East Forest */
{
	terrain_type VAR_TERRAIN_FOREST
	base_elevation 3
	land_percent 100
	top_border 90
}
create_land /* North-West Forest */
{
	terrain_type VAR_TERRAIN_FOREST
	base_elevation 3
	land_percent 100
	bottom_border 90
}

if 2_PLAYER_GAME
	create_land /* T1 */
	{
		terrain_type VAR_TERRAIN_BASE
		number_of_tiles 300
		if VAR_TEAMPLACEMENT_TEAMSIDE
			land_position 75 50
		else 
			land_position 25 50
		endif
		assign_to_player 1
	}
	create_land /* T2 */
	{
		terrain_type VAR_TERRAIN_BASE
		number_of_tiles 300
		if VAR_TEAMPLACEMENT_TEAMSIDE
			land_position 25 50
		else 
			land_position 75 50
		endif
		assign_to_player 2
	}
else
	if TEAM1_SIZE2
		if VAR_TEAMPLACEMENT_TEAM1ORDER
			create_land /* T1-B */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 75 70
				else 
					land_position 25 70
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-T */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 75 30
				else 
					land_position 25 30
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		else
			create_land /* T1-T */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 75 30
				else 
					land_position 25 30
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-B */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 75 70
				else 
					land_position 25 70
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		endif
	elseif TEAM1_SIZE3
		if VAR_TEAMPLACEMENT_TEAM1ORDER
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 70 80
				else 
					land_position 30 80
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-P */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 85 50
				else 
					land_position 15 50
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 70 20
				else 
					land_position 30 20
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		else
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 70 20
				else 
					land_position 30 20
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-P */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 85 50
				else 
					land_position 15 50
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 70 80
				else 
					land_position 30 80
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		endif
	elseif TEAM1_SIZE4
		if VAR_TEAMPLACEMENT_TEAM1ORDER
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 65 20
				else 
					land_position 35 20
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 80 40
				else 
					land_position 20 40
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 80 60
				else 
					land_position 20 60
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 65 80
				else 
					land_position 35 80
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		else
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 65 80
				else 
					land_position 35 80
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 80 60
				else 
					land_position 20 60
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 80 40
				else 
					land_position 20 40
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 65 20
				else 
					land_position 35 20
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		endif
	else
		create_land /* T1 */
		{
			terrain_type VAR_TERRAIN_BASE
			number_of_tiles 300
			if VAR_TEAMPLACEMENT_TEAMSIDE
				land_position 75 50
			else 
				land_position 25 50
			endif
			assign_to AT_TEAM 0 0 0
		}
	endif

	if TEAM2_SIZE2
		if VAR_TEAMPLACEMENT_TEAM2ORDER
			create_land /* T2-B */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 25 70
				else 
					land_position 75 70
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-T */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 25 30
				else 
					land_position 75 30
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		else
			create_land /* T2-T */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 25 30
				else 
					land_position 75 30
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-B */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 25 70
				else 
					land_position 75 70
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		endif
	elseif TEAM2_SIZE3
		if VAR_TEAMPLACEMENT_TEAM2ORDER
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 30 80
				else 
					land_position 70 80
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-P */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 15 50
				else
					land_position 85 50
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 30 20
				else 
					land_position 70 20
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		else
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 30 20
				else 
					land_position 70 20
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-P */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 15 50
				else
					land_position 85 50
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 30 80
				else 
					land_position 70 80
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		endif
	elseif TEAM2_SIZE4
		if VAR_TEAMPLACEMENT_TEAM2ORDER
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 35 20
				else 
					land_position 65 20
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 20 40
				else 
					land_position 80 40
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 20 60
				else 
					land_position 80 60
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 35 80
				else 
					land_position 65 80
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		else
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 35 80
				else 
					land_position 65 80
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 20 60
				else 
					land_position 80 60
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BP */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 20 40
				else 
					land_position 80 40
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_BASE
				number_of_tiles 300
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position 35 20
				else 
					land_position 65 20
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		endif
	else
		create_land /* T2 */
		{
			terrain_type VAR_TERRAIN_BASE
			number_of_tiles 300
			if VAR_TEAMPLACEMENT_TEAMSIDE
				land_position 25 50
			else 
				land_position 75 50
			endif
			assign_to AT_TEAM 0 0 0
		}
	endif
endif

<ELEVATION_GENERATION>
enable_balanced_elevation

create_elevation 2
{
	base_terrain VAR_TERRAIN_BASE
	number_of_tiles 2048
	number_of_clumps 6
	set_scale_by_size
	set_scale_by_groups
	spacing 5
}


<TERRAIN_GENERATION>

create_terrain VAR_TERRAIN_FOREST
{
	base_terrain VAR_TERRAIN_BASE
	land_percent 8
	number_of_clumps 16
	set_scale_by_groups
	spacing_to_other_terrain_types 5
	set_avoid_player_start_areas
}

/*<CONNECTION_GENERATION>*/

<OBJECTS_GENERATION> 

/* Player Thingies */
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object SCOUT
{
	number_of_objects 1
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}
create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 6
}

if DEATH_MATCH
elseif EMPIRE_WARS
else
	/* Sheeps */
	create_object SHEEP /* Close ones */
	{
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 8
		find_closest
		number_of_objects 3
		set_gaia_object_only
		set_tight_grouping
		actor_area 999
		actor_area_radius 1
		set_circular_placement
	}
	create_object SHEEP /* Close ones */
	{
		set_place_for_every_player
		find_closest
		set_tight_grouping
		actor_area_to_place_in 999
		set_circular_placement
	}
	create_object SHEEP /* Further ones */
	{
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		number_of_objects 2
		number_of_groups 2
		temp_min_distance_group_placement 10
		set_gaia_object_only
		set_tight_grouping
		set_circular_placement
	}

	/* Deers */
	create_object DLC_IBEX
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 20
		number_of_objects 4
		set_gaia_object_only
		set_tight_grouping
		set_circular_placement
	}

	/* Boars */
	create_object BOAR
	{
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		number_of_groups 2
		set_gaia_object_only
		temp_min_distance_group_placement 2
		avoid_forest_zone 1
		set_circular_placement
	}

	/* Bushes */
	create_object FORAGE_BUSH
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 12
		number_of_objects 6
		set_gaia_object_only
		set_tight_grouping
		avoid_forest_zone 2
		actor_area 1
		actor_area_radius 5
		avoid_all_actor_areas
		set_circular_placement
	}
	
	/* Player Stragglers */
	create_object VAR_OBJECT_STRAGGLERTREE
	{
		number_of_groups 3
		set_place_for_every_player
		min_distance_to_players 4
		max_distance_to_players 6
		set_gaia_object_only
		terrain_to_place_on VAR_TERRAIN_BASE
		temp_min_distance_group_placement 7
	}
endif

/* Main Golds */
create_object GOLD
{
	set_place_for_every_player
	min_distance_to_players 13
	max_distance_to_players 15
	number_of_objects 2
	number_of_groups 2
	temp_min_distance_group_placement 18
	resource_delta 200 /* +800 = 1000 */
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 2
	actor_area_radius 5
	avoid_all_actor_areas
}

/* Main Stone */
create_object STONE
{
	set_place_for_every_player
	min_distance_to_players 13
	max_distance_to_players 15
	number_of_objects 2
	number_of_groups 2
	temp_min_distance_group_placement 18
	resource_delta -50 /* +350 = 300 */
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 3
	actor_area_radius 5
	avoid_all_actor_areas
}

/* Extra golds */
create_object GOLD
{
	terrain_to_place_on VAR_TERRAIN_BASE
	min_distance_to_players 25
	number_of_objects 1
	number_of_groups 8
	set_scaling_to_player_number
	resource_delta 2950 /* +800 = 3750 */
	avoid_forest_zone 4
	temp_min_distance_group_placement 15
	actor_area 10
	actor_area_radius 5
	avoid_all_actor_areas
}

/* Extra stones */
create_object STONE
{
	terrain_to_place_on VAR_TERRAIN_BASE
	min_distance_to_players 25
	number_of_objects 1
	number_of_groups 4
	set_scaling_to_player_number
	resource_delta 150 /* +350 = 500 */
	avoid_forest_zone 4
	temp_min_distance_group_placement 15
	actor_area 11
	actor_area_radius 5
	avoid_all_actor_areas
}

/* Extra stragglers */
create_object VAR_OBJECT_STRAGGLERTREE
{
	number_of_groups 500
	set_scale_by_groups
	min_distance_to_players 12
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_BASE
	temp_min_distance_group_placement 6
}

/* Relics */
create_object RELIC
{
	terrain_to_place_on VAR_TERRAIN_BASE
	min_distance_to_players 25
	number_of_objects 1
	number_of_groups 1
	set_scaling_to_player_number
	avoid_forest_zone 4
	temp_min_distance_group_placement 25
	actor_area 100
	actor_area_radius 25
}
create_object RELIC
{
	terrain_to_place_on VAR_TERRAIN_BASE
	min_distance_to_players 25
	number_of_objects 1
	number_of_groups 3
	avoid_forest_zone 4
	temp_min_distance_group_placement 25
	avoid_actor_area 100
}

/* King and castle for regicide */
if VAR_GAMEMODE_REG
	create_object KING
	{
		number_of_objects 1
		set_place_for_every_player
		min_distance_to_players 7
		max_distance_to_players 9
		set_circular_placement
	}
	create_object CASTLE
	{
		number_of_objects 1
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		avoid_all_actor_areas
		set_circular_placement
	}
endif
