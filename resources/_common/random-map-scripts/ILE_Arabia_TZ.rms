/* === Arabia (v0.1) ===
MADE BY ILEVELIN

Supported players/teams configuration:
	ANY
	
Supported map sizes:
	2p
	3p
	4p
	6p
	8p
	
Supported game modes:
	ANY except Empire Wars
	
NOTE: 
	Playing with any non-supported player configuration, map size and/or game mode may place
	the players in unbalanced positions, bug the terrain generation or even not load. 
	Support to some other configurations MAY be introduced in the future.

Scripting references:
	https://docs.aoe2map.net/ 

Changelog: [ +:Added | -:Removed | *:Adjusted | !:Fixed ]
	( v0.1 ) Unpublished
		+ Hello World!

*/

/* *** DEBUG CONFIGS *** */
/* #define VAR_DEBUG_FORCE_RM */
/* #define VAR_DEBUG_FORCE_EW */
/* #define VAR_DEBUG_FORCE_DM */

/* #define VAR_DEBUG_FORCE_REG */

/* #define VAR_DEBUG_FOREST_AREAS */
/* #define VAR_DEBUG_FORCEFOREST_11 */
/* #define VAR_DEBUG_FORCEFOREST_12 */
/* #define VAR_DEBUG_FORCEFOREST_21 */
/* #define VAR_DEBUG_FORCEFOREST_22 */

/* TERRAINS */
#const VAR_TERRAIN_BASE 6 /* Dirt */
#const VAR_TERRAIN_PLACEHOLDER1 0 /* GRASS */
#const VAR_TERRAIN_PLACEHOLDER2 11 /* DIRT2 */
#const VAR_TERRAIN_PLACEHOLDER3 42 /* DLC_DIRT4 */
#const VAR_TERRAIN_PLACEHOLDER4 76 /* Dirt, Mud */

#const VAR_TERRAIN_PLACEHOLDER_TMP 85 /* placeholder */

#const VAR_TERRAIN_DECORATOR_1 41 /* DLC_SAVANNAH */
#const VAR_TERRAIN_DECORATOR_2 0 /* GRASS */
#const VAR_TERRAIN_DECORATOR_2w 58 /* DLC_WATER5 */

#const VAR_TERRAIN_FOREST 48 /* DLC_DRAGONFOREST */

/* OBJECTS */
#const VAR_OBJECT_STRAGGLERTREE 1051 /* DLC_DRAGONTREE */

#const VAR_OBJECT_PLACEHOLDER_SMALL 600 /* Flag A */
effect_amount SET_ATTRIBUTE VAR_OBJECT_PLACEHOLDER_SMALL ATTR_HITPOINTS 0

#const VAR_OBJECT_PLACEHOLDER_BIG 1376 /* Torch B */
effect_amount SET_ATTRIBUTE VAR_OBJECT_PLACEHOLDER_BIG ATTR_HITPOINTS 0

#const VAR_OBJECT_DOCK_BLOCKER 1654
effect_percent GAIA_SET_ATTRIBUTE VAR_OBJECT_DOCK_BLOCKER ATTR_RADIUS_1 50
effect_percent GAIA_SET_ATTRIBUTE VAR_OBJECT_DOCK_BLOCKER ATTR_RADIUS_2 50

#const VAR_OBJECT_LAZY_MALE 1285 /* FE Flag */
effect_amount SET_ATTRIBUTE VAR_OBJECT_LAZY_MALE ATTR_DEAD_ID 83
effect_amount SET_ATTRIBUTE VAR_OBJECT_LAZY_MALE ATTR_HITPOINTS 0

#const VAR_OBJECT_LAZY_FEMALE 851 /* ES Flag */
effect_amount SET_ATTRIBUTE VAR_OBJECT_LAZY_FEMALE ATTR_DEAD_ID 293
effect_amount SET_ATTRIBUTE VAR_OBJECT_LAZY_FEMALE ATTR_HITPOINTS 0

/* GAMEMODE DETECTION */
if VAR_DEBUG_FORCE_RM
elseif VAR_DEBUG_FORCE_EW
	#define VAR_GAMEMODE_EW
elseif VAR_DEBUG_FORCE_DM
	#define VAR_GAMEMODE_DM
elseif EMPIRE_WARS
	#define VAR_GAMEMODE_EW
elseif DEATH_MATCH
	#define VAR_GAMEMODE_DM
endif

if VAR_DEBUG_FORCE_REG
	#define VAR_GAMEMODE_REG
elseif REGICIDE
	#define VAR_GAMEMODE_REG
endif

/* OTHER STUFF */
if VAR_DEBUG_FORCEFOREST_11
elseif VAR_DEBUG_FORCEFOREST_12
	#define VAR_BACKFOREST2
elseif VAR_DEBUG_FORCEFOREST_21
	#define VAR_FRONTFOREST2
elseif VAR_DEBUG_FORCEFOREST_22
	#define VAR_BACKFOREST2
	#define VAR_FRONTFOREST2
else
	start_random
		percent_chance 25
			#define VAR_BACKFOREST2
		percent_chance 25
			#define VAR_BACKFOREST2
			#define VAR_FRONTFOREST2
		percent_chance 25
			#define VAR_FRONTFOREST2
	end_random
endif

if 8_PLAYER_GAME
	#define VAR_PLAYEREXISTS_1
	#define VAR_PLAYEREXISTS_2
	#define VAR_PLAYEREXISTS_3
	#define VAR_PLAYEREXISTS_4
	#define VAR_PLAYEREXISTS_5
	#define VAR_PLAYEREXISTS_6
	#define VAR_PLAYEREXISTS_7
	#define VAR_PLAYEREXISTS_8
elseif 7_PLAYER_GAME
	#define VAR_PLAYEREXISTS_1
	#define VAR_PLAYEREXISTS_2
	#define VAR_PLAYEREXISTS_3
	#define VAR_PLAYEREXISTS_4
	#define VAR_PLAYEREXISTS_5
	#define VAR_PLAYEREXISTS_6
	#define VAR_PLAYEREXISTS_7
elseif 6_PLAYER_GAME
	#define VAR_PLAYEREXISTS_1
	#define VAR_PLAYEREXISTS_2
	#define VAR_PLAYEREXISTS_3
	#define VAR_PLAYEREXISTS_4
	#define VAR_PLAYEREXISTS_5
	#define VAR_PLAYEREXISTS_6
elseif 5_PLAYER_GAME
	#define VAR_PLAYEREXISTS_1
	#define VAR_PLAYEREXISTS_2
	#define VAR_PLAYEREXISTS_3
	#define VAR_PLAYEREXISTS_4
	#define VAR_PLAYEREXISTS_5
elseif 4_PLAYER_GAME
	#define VAR_PLAYEREXISTS_1
	#define VAR_PLAYEREXISTS_2
	#define VAR_PLAYEREXISTS_3
	#define VAR_PLAYEREXISTS_4
elseif 3_PLAYER_GAME
	#define VAR_PLAYEREXISTS_1
	#define VAR_PLAYEREXISTS_2
	#define VAR_PLAYEREXISTS_3
elseif 2_PLAYER_GAME
	#define VAR_PLAYEREXISTS_1
	#define VAR_PLAYEREXISTS_2
elseif 1_PLAYER_GAME
	#define VAR_PLAYEREXISTS_1
endif


<PLAYER_SETUP>
behavior_version 2

<LAND_GENERATION>
base_terrain VAR_TERRAIN_PLACEHOLDER1

create_player_lands
{
	terrain_type VAR_TERRAIN_PLACEHOLDER1

	if TINY_MAP
		circle_radius 34 0
	elseif SMALL_MAP
		circle_radius 34 0
	elseif MEDIUM_MAP
		circle_radius 35 0
	elseif LARGE_MAP
		circle_radius 37 0
	else
		circle_radius 38 0
	endif
	
	number_of_tiles 0
	base_size 1
	border_fuzziness 100
}

create_land
{
	terrain_type VAR_TERRAIN_PLACEHOLDER2

	if TINY_MAP
		left_border 22
		right_border 22
		top_border 22
		bottom_border 22
	elseif SMALL_MAP
		left_border 21
		right_border 21
		top_border 21
		bottom_border 21
	elseif MEDIUM_MAP
		left_border 20
		right_border 20
		top_border 20
		bottom_border 20
	elseif LARGE_MAP
		left_border 19
		right_border 19
		top_border 19
		bottom_border 19
	else
		left_border 18
		right_border 18
		top_border 18
		bottom_border 18
	endif
	
	land_percent 100
	border_fuzziness 100
}

<ELEVATION_GENERATION>
create_elevation 3
{
	base_terrain VAR_TERRAIN_PLACEHOLDER1
	number_of_tiles 4000
	number_of_clumps 80
	set_scale_by_size
	set_scale_by_groups
	enable_balanced_elevation
}
create_elevation 3
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	number_of_tiles 1000
	number_of_clumps 20
	set_scale_by_size
	set_scale_by_groups
	enable_balanced_elevation
}

<TERRAIN_GENERATION>

/* INNER TERRAIN BASE TERRAIN FILL */
create_terrain VAR_TERRAIN_BASE
{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 set_avoid_player_start_areas 30 number_of_clumps 9320 set_scale_by_groups }

/* OUTER AREA BASE TERRAIN FILL */
create_terrain VAR_TERRAIN_BASE
{ base_terrain VAR_TERRAIN_PLACEHOLDER1 land_percent 100 set_avoid_player_start_areas 28 number_of_clumps 9320 set_scale_by_groups }


/* INNER FOREST(S) PLAYER 1 */
if VAR_PLAYEREXISTS_1

	create_terrain VAR_TERRAIN_PLACEHOLDER3
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 clumping_factor 30 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_FRONTFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER4
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER4 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
	endif
endif

/* INNER FOREST(S) PLAYER 2 */
if VAR_PLAYEREXISTS_2

	create_terrain VAR_TERRAIN_PLACEHOLDER3
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 clumping_factor 30 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_FRONTFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER4
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER4 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
	endif
endif

/* INNER FOREST(S) PLAYER 3 */
if VAR_PLAYEREXISTS_3

	create_terrain VAR_TERRAIN_PLACEHOLDER3
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 clumping_factor 30 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_FRONTFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER4
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER4 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
	endif
endif

/* INNER FOREST(S) PLAYER 4 */
if VAR_PLAYEREXISTS_4

	create_terrain VAR_TERRAIN_PLACEHOLDER3
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 clumping_factor 30 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_FRONTFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER4
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER4 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
	endif
endif

/* INNER FOREST(S) PLAYER 5 */
if VAR_PLAYEREXISTS_5

	create_terrain VAR_TERRAIN_PLACEHOLDER3
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 clumping_factor 30 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_FRONTFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER4
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER4 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
	endif
endif

/* INNER FOREST(S) PLAYER 6 */
if VAR_PLAYEREXISTS_6

	create_terrain VAR_TERRAIN_PLACEHOLDER3
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 clumping_factor 30 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_FRONTFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER4
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER4 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
	endif
endif

/* INNER FOREST(S) PLAYER 7 */
if VAR_PLAYEREXISTS_7

	create_terrain VAR_TERRAIN_PLACEHOLDER3
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 clumping_factor 30 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_FRONTFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER4
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER4 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
	endif
endif

/* INNER FOREST(S) PLAYER 8 */
if VAR_PLAYEREXISTS_8

	create_terrain VAR_TERRAIN_PLACEHOLDER3
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 clumping_factor 30 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_FRONTFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER4
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER4 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
	endif
endif

/* PLACEHOLDER LEFTOVERS CLEANUP */

if VAR_DEBUG_FOREST_AREAS
else
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
endif

/* OUTER FOREST(S) PLAYER 1 */
if VAR_PLAYEREXISTS_1
	create_terrain VAR_TERRAIN_PLACEHOLDER2
	{ base_terrain VAR_TERRAIN_PLACEHOLDER1 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_BACKFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER3
			{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	endif
endif

/* OUTER FOREST(S) PLAYER 2 */
if VAR_PLAYEREXISTS_2
	create_terrain VAR_TERRAIN_PLACEHOLDER2
	{ base_terrain VAR_TERRAIN_PLACEHOLDER1 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_BACKFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER3
			{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	endif
endif

/* OUTER FOREST(S) PLAYER 3 */
if VAR_PLAYEREXISTS_3
	create_terrain VAR_TERRAIN_PLACEHOLDER2
	{ base_terrain VAR_TERRAIN_PLACEHOLDER1 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_BACKFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER3
			{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	endif
endif

/* OUTER FOREST(S) PLAYER 4 */
if VAR_PLAYEREXISTS_4
	create_terrain VAR_TERRAIN_PLACEHOLDER2
	{ base_terrain VAR_TERRAIN_PLACEHOLDER1 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_BACKFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER3
			{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	endif
endif

/* OUTER FOREST(S) PLAYER 5 */
if VAR_PLAYEREXISTS_5
	create_terrain VAR_TERRAIN_PLACEHOLDER2
	{ base_terrain VAR_TERRAIN_PLACEHOLDER1 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_BACKFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER3
			{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	endif
endif

/* OUTER FOREST(S) PLAYER 6 */
if VAR_PLAYEREXISTS_6
	create_terrain VAR_TERRAIN_PLACEHOLDER2
	{ base_terrain VAR_TERRAIN_PLACEHOLDER1 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_BACKFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER3
			{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	endif
endif

/* OUTER FOREST(S) PLAYER 7 */
if VAR_PLAYEREXISTS_7
	create_terrain VAR_TERRAIN_PLACEHOLDER2
	{ base_terrain VAR_TERRAIN_PLACEHOLDER1 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_BACKFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER3
			{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	endif
endif

/* OUTER FOREST(S) PLAYER 8 */
if VAR_PLAYEREXISTS_8
	create_terrain VAR_TERRAIN_PLACEHOLDER2
	{ base_terrain VAR_TERRAIN_PLACEHOLDER1 land_percent 100 }

	if VAR_DEBUG_FOREST_AREAS
	else
		create_terrain VAR_TERRAIN_FOREST
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 2 }

		if VAR_BACKFOREST2
			create_terrain VAR_TERRAIN_PLACEHOLDER3
			{ base_terrain VAR_TERRAIN_PLACEHOLDER2 number_of_tiles 75 set_avoid_player_start_areas 18 spacing_to_other_terrain_types 3 }

			create_terrain VAR_TERRAIN_FOREST
			{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
		endif

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }

		create_terrain VAR_TERRAIN_BASE
		{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	endif
endif

/* PLACEHOLDER LEFTOVERS CLEANUP */
if VAR_DEBUG_FOREST_AREAS
else
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER1 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER1 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER1 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER1 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER2 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
	create_terrain VAR_TERRAIN_BASE
	{ base_terrain VAR_TERRAIN_PLACEHOLDER3 land_percent 100 number_of_clumps 999 }
endif

/* Extra Woods */
if VAR_DEBUG_FOREST_AREAS
else
	create_terrain VAR_TERRAIN_FOREST
	{
		base_terrain VAR_TERRAIN_BASE
		land_percent 8
		number_of_clumps 14
		set_scale_by_groups
		set_avoid_player_start_areas 35
	}
endif

/* Decorations */
create_terrain VAR_TERRAIN_DECORATOR_1
{
	base_terrain VAR_TERRAIN_BASE
	land_percent 12
	number_of_clumps 20
	set_scale_by_groups
}

create_terrain VAR_TERRAIN_DECORATOR_2
{
	base_terrain VAR_TERRAIN_BASE
	set_avoid_player_start_areas 35
	land_percent 1
	number_of_clumps 6
	set_scale_by_groups
	terrain_mask 1
}
create_terrain VAR_TERRAIN_DECORATOR_2w
{
	base_terrain VAR_TERRAIN_BASE
	base_layer VAR_TERRAIN_DECORATOR_2
	land_percent 99
	number_of_clumps 999
	spacing_to_other_terrain_types 1
	terrain_mask 1
}
/*
<CONNECTION_GENERATION>
*/

<OBJECTS_GENERATION> 
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}

create_object SCOUT
{
	set_place_for_every_player
	min_distance_to_players 5
	max_distance_to_players 7
	if VAR_SCOUT_AMOUNT_1
		number_of_objects 1
	elseif VAR_SCOUT_AMOUNT_2
		number_of_objects 2
	endif
	temp_min_distance_group_placement 7
}

create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players 5
	max_distance_to_players 7
}

/* Main Gold */
create_object GOLD
{
	set_place_for_every_player
	min_distance_to_players 12
	max_distance_to_players 12
	number_of_objects 8
	temp_min_distance_group_placement 9
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 4
	actor_area 2
	actor_area_radius 10
	terrain_to_place_on VAR_TERRAIN_BASE
	find_closest_to_map_center
	set_circular_placement
}

/* Main Stone */
create_object STONE
{
	set_place_for_every_player
	min_distance_to_players 12
	max_distance_to_players 12
	number_of_objects 5
	temp_min_distance_group_placement 15
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 3
	actor_area 3
	actor_area_radius 7
	avoid_actor_area 2
	terrain_to_place_on VAR_TERRAIN_BASE
	find_closest_to_map_center
	set_circular_placement
}

/* Secondary Golds */
create_object GOLD
{
	set_place_for_every_player
	min_distance_to_players 15
	max_distance_to_players 18
	number_of_objects 4
	temp_min_distance_group_placement 9
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 3
	actor_area 2
	actor_area_radius 5
	avoid_all_actor_areas
	terrain_to_place_on VAR_TERRAIN_BASE
	find_closest_to_map_center
	set_circular_placement
}
create_object GOLD
{
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 18
	number_of_objects 4
	temp_min_distance_group_placement 9
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 3
	actor_area 2
	actor_area_radius 5
	avoid_all_actor_areas
	terrain_to_place_on VAR_TERRAIN_BASE
	find_closest_to_map_edge
	set_circular_placement
}

/* Secondary Stone */
create_object STONE
{
	set_place_for_every_player
	min_distance_to_players 15
	max_distance_to_players 18
	number_of_objects 4
	temp_min_distance_group_placement 15
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 3
	actor_area 3
	actor_area_radius 4
	avoid_all_actor_areas
	terrain_to_place_on VAR_TERRAIN_BASE
	find_closest_to_map_edge
	set_circular_placement
}

if DEATH_MATCH
elseif EMPIRE_WARS
else
	/* Sheeps */
	create_object SHEEP /* Close ones */
	{
		set_place_for_every_player
		min_distance_to_players 8
		max_distance_to_players 10
		number_of_objects 3
		set_gaia_object_only
		set_tight_grouping
		actor_area 100
		actor_area_radius 1
		set_circular_placement
	}
	create_object SHEEP /* Close ones */
	{
		set_place_for_every_player
		find_closest
		number_of_objects 1
		actor_area_to_place_in 100
		set_circular_placement
	}
	create_object SHEEP /* Further ones */
	{
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		number_of_objects 2
		number_of_groups 2
		temp_min_distance_group_placement 10
		set_gaia_object_only
		set_tight_grouping
		set_circular_placement
	}

	/* Boars */
	create_object DLC_RHINO
	{
		set_place_for_every_player
		min_distance_to_players 12
		max_distance_to_players 12
		number_of_groups 1
		set_gaia_object_only
		avoid_forest_zone 1
		find_closest_to_map_center
		terrain_to_place_on VAR_TERRAIN_BASE
		set_circular_placement
	}
	create_object DLC_RHINO
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 14
		number_of_groups 1
		set_gaia_object_only
		avoid_forest_zone 1
		find_closest_to_map_edge
		terrain_to_place_on VAR_TERRAIN_BASE
		set_circular_placement
	}
	
	/* Deers */
	create_object DLC_IBEX
	{
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 18
		number_of_objects 3
		set_tight_grouping
		set_gaia_object_only
		terrain_to_place_on VAR_TERRAIN_BASE
		set_circular_placement
	}

	/* Bushes */
	create_object FORAGE_BUSH
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 12
		number_of_objects 6
		set_gaia_object_only
		set_tight_grouping
		avoid_forest_zone 2
		actor_area 1
		actor_area_radius 5
		avoid_all_actor_areas
		set_circular_placement
	}
	
	/* Player Stragglers */
	create_object VAR_OBJECT_STRAGGLERTREE
	{
		number_of_groups 6
		set_place_for_every_player
		min_distance_to_players 4
		max_distance_to_players 6
		set_gaia_object_only
		terrain_to_place_on VAR_TERRAIN_BASE
		temp_min_distance_group_placement 3
	}
endif

/* Extra Golds */
create_object GOLD
{
	min_distance_to_players 30
	number_of_objects 3
	number_of_groups 3
	set_scaling_to_player_number
	temp_min_distance_group_placement 25
	min_distance_to_map_edge 5
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 10
	actor_area_radius 15
	avoid_all_actor_areas
}

/* Extra Stones */
create_object STONE
{
	min_distance_to_players 25
	number_of_objects 2
	number_of_groups 2
	set_scaling_to_player_number
	temp_min_distance_group_placement 25
	min_distance_to_map_edge 5
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 11
	actor_area_radius 15
	avoid_all_actor_areas
}

/* Extra Bushes and deers */
create_object FORAGE_BUSH
{
	min_distance_to_players 35
	number_of_objects 2
	number_of_groups 2
	set_scaling_to_player_number
	temp_min_distance_group_placement 25
	min_distance_to_map_edge 5
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 12
	actor_area_radius 5
	avoid_all_actor_areas
}
create_object DLC_IBEX
{
	number_of_objects 2
	number_of_groups 2
	set_scaling_to_player_number
	set_gaia_object_only
	actor_area_to_place_in 12
	temp_min_distance_group_placement 20
	terrain_to_place_on VAR_TERRAIN_BASE
	set_tight_grouping
	force_placement
}

/* Relics */
create_object RELIC
{
	terrain_to_place_on VAR_TERRAIN_BASE
	min_distance_to_players 40
	number_of_objects 1
	avoid_forest_zone 3
	find_closest_to_map_center
	actor_area 99
	actor_area_radius 30
	min_distance_to_map_edge 10
}
create_object RELIC
{
	terrain_to_place_on VAR_TERRAIN_BASE
	min_distance_to_players 40
	number_of_objects 1
	avoid_forest_zone 3
	actor_area 98
	actor_area_radius 30
	avoid_actor_area 99
	min_distance_to_map_edge 10
}
create_object RELIC
{
	terrain_to_place_on VAR_TERRAIN_BASE
	min_distance_to_players 40
	number_of_objects 1
	avoid_forest_zone 3
	temp_min_distance_group_placement 50
	actor_area 97
	actor_area_radius 30
	avoid_actor_area 99
	avoid_actor_area 98
	min_distance_to_map_edge 10
}
create_object RELIC
{
	terrain_to_place_on VAR_TERRAIN_BASE
	min_distance_to_players 20
	max_distance_to_players 30
	number_of_objects 1
	number_of_groups 1
	set_place_for_every_player
	set_gaia_object_only
	avoid_forest_zone 3
	temp_min_distance_group_placement 25
	avoid_actor_area 99
	avoid_actor_area 98
	avoid_actor_area 97
	min_distance_to_map_edge 10
}

/* Extra stragglers */
create_object VAR_OBJECT_STRAGGLERTREE
{
	number_of_groups 75
	set_scale_by_groups
	min_distance_to_players 20
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_BASE
	temp_min_distance_group_placement 3
}

/* King and castle for regicide */
if VAR_GAMEMODE_REG
	create_object KING
	{
		number_of_objects 1
		set_place_for_every_player
		min_distance_to_players 7
		max_distance_to_players 9
		set_circular_placement
	}
	create_object CASTLE
	{
		number_of_objects 1
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		avoid_all_actor_areas
		set_circular_placement
	}
endif