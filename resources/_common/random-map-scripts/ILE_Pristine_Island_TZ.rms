/* === Pristine Island (v0.5) ===
MADE BY ILEVELIN

Supported players/teams configuration:
	ANY
	
Supported map sizes:
	2p
	3p
	4p
	6p
	8p
	
Supported game modes:
	ANY except Empire Wars
	
NOTE: 
	Playing with any non-supported player configuration, map size and/or game mode may place
	the players in unbalanced positions, bug the terrain generation or even not load. 
	Support to some other configurations MAY be introduced in the future.
	
Scripting references:
	https://docs.aoe2map.net/
	
Changelog: [ +:Added | -:Removed | *:Adjusted | !:Fixed ]
	( v0.5 ) 2023/03/26
		+ Elevated the center of the map
		+ Added relics (as was intended)
		* Changed most per-player objects to spawn in a circular raidus instead of a square one
		* Reduced max distance of deer to each player
	( v0.4 ) 2022/12/16
		+ Made one of the main cows automatically captured
		* Divided the secondary cow in 3 individual cows instead of one group of 3
	( v0.3 ) 2021/09/23
		+ Added forage bushes to the central zone (7 per player)
		+ Increased number of straggler trees in the central zone
	( v0.2 ) 202?/??/??
		+ Increased the island size and the central zone size
		+ Increased number of ibex in the central zone per player from 3 to 7
		+ Added 6 forage bushes to each player
		+ Increased fish density
		- Reduced starting cows from 8 to 6
		* Reduced the distance betweet the boars and the town center
	( v0.1 ) 2020/10/26
		+ Hello World!
*/
 
#const VAR_TERRAIN_BASE 0 /* GRASS */
#const VAR_TERRAIN_CENTER 100 /* DLC_DRYGRASS */
#const VAR_TERRAIN_FOREST 10 /* FOREST */
#const VAR_TERRAIN_WATER 1 /* WATER */
#const VAR_TERRAIN_WATER_DEEP 57 /* DLC_WATER4 */

#const VAR_OBJECT_STRAGGLERTREE 1051 /* DLC_DRAGONTREE */
/*
<PLAYER_SETUP>
*/

<LAND_GENERATION>
base_terrain VAR_TERRAIN_BASE

create_player_lands
{
	terrain_type VAR_TERRAIN_BASE
	number_of_tiles 100
	base_size 3
	border_fuzziness 20
	clumping_factor 50
	circle_radius 27
}
create_land 
{
	terrain_type VAR_TERRAIN_BASE
	land_percent 100
	base_elevation 2
	if TINY_MAP
		top_border 37
		bottom_border 37
		left_border 37
		right_border 37
	elseif SMALL_MAP
		top_border 36
		bottom_border 36
		left_border 36
		right_border 36
	elseif MEDIUM_MAP
		top_border 35
		bottom_border 35
		left_border 35
		right_border 35
	elseif LARGE_MAP
		top_border 34
		bottom_border 34
		left_border 34
		right_border 34
	else
		top_border 33
		bottom_border 33
		left_border 33
		right_border 33
	endif
}

<ELEVATION_GENERATION>


<TERRAIN_GENERATION>
create_terrain VAR_TERRAIN_CENTER
{
	base_terrain VAR_TERRAIN_BASE
	height_limits 1 3
	land_percent 100
}

create_terrain VAR_TERRAIN_WATER
{
	base_terrain VAR_TERRAIN_BASE
	land_percent 100
	if TINY_MAP
		spacing_to_other_terrain_types 26
	elseif SMALL_MAP
		spacing_to_other_terrain_types 31
	elseif MEDIUM_MAP
		spacing_to_other_terrain_types 36
	elseif LARGE_MAP
		spacing_to_other_terrain_types 41
	else
		spacing_to_other_terrain_types 46
	endif
}

create_terrain VAR_TERRAIN_WATER_DEEP
{
	base_terrain WATER
	land_percent 100
	spacing_to_other_terrain_types 2
}

create_terrain VAR_TERRAIN_FOREST /* Wood please */
{
	base_terrain VAR_TERRAIN_BASE
	if TINY_MAP
		number_of_tiles 800
		number_of_clumps 15
	elseif SMALL_MAP
		number_of_tiles 1300
		number_of_clumps 25
	elseif MEDIUM_MAP
		number_of_tiles 1600
		number_of_clumps 35
	elseif LARGE_MAP
		number_of_tiles 1900
		number_of_clumps 45
	else
		number_of_tiles 2200
		number_of_clumps 55
	endif
	clumping_factor 35
	spacing_to_other_terrain_types 6
	set_avoid_player_start_areas
}
/*
<CONNECTION_GENERATION>
*/
<OBJECTS_GENERATION> 
/* Player Thingies */
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object SCOUT
{
	number_of_objects 1
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}
create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 6
}
/* Main Stones */
create_object STONE
{
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 20
	number_of_objects 4
	number_of_groups 2
	temp_min_distance_group_placement 15
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 3
	actor_area_radius 4
	terrain_to_place_on VAR_TERRAIN_BASE
	set_circular_placement
}

/* Main Golds */
create_object GOLD
{
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 15
	number_of_objects 4
	number_of_groups 2
	temp_min_distance_group_placement 9
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 2
	actor_area_radius 5
	avoid_all_actor_areas
	terrain_to_place_on VAR_TERRAIN_BASE
	set_circular_placement
}

if DEATH_MATCH
else
	/* Cows */
	create_object DLC_COW /* Close ones */
	{
		set_place_for_every_player
		min_distance_to_players 5
		max_distance_to_players 8
		find_closest
		number_of_objects 2
		set_gaia_object_only
		set_tight_grouping
		actor_area 999
		actor_area_radius 1
		set_circular_placement
	}
	create_object DLC_COW
	{
		set_place_for_every_player
		find_closest
		set_tight_grouping
		actor_area_to_place_in 999
	}
	create_object DLC_COW /* Further ones */
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 12
		number_of_groups 3
		set_gaia_object_only
		set_tight_grouping
		set_circular_placement
	}

	/* Player Stragglers */
	create_object VAR_OBJECT_STRAGGLERTREE
	{
		number_of_groups 3
		set_place_for_every_player
		min_distance_to_players 4
		max_distance_to_players 6
		set_gaia_object_only
		terrain_to_place_on VAR_TERRAIN_BASE
		temp_min_distance_group_placement 7
	}
	
	/* Bushes */
	create_object FORAGE_BUSH
	{
		set_place_for_every_player
		min_distance_to_players 8
		max_distance_to_players 10
		number_of_objects 6
		set_gaia_object_only
		set_tight_grouping
		avoid_forest_zone 2
		actor_area 1
		actor_area_radius 5
		avoid_all_actor_areas
		terrain_to_place_on VAR_TERRAIN_BASE
		set_circular_placement
	}

endif


/* Deers */
create_object DLC_IBEX
{
	set_place_for_every_player
	max_distance_to_players 30
	number_of_objects 1
	number_of_groups 7
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_CENTER
	
	if TINY_MAP
		min_distance_to_players 15
	elseif SMALL_MAP
		min_distance_to_players 18
	elseif MEDIUM_MAP
		min_distance_to_players 23
	elseif LARGE_MAP
		min_distance_to_players 27
	else
		min_distance_to_players 30
	endif
}

/* Extra Berries */
create_object FORAGE_BUSH
{
	number_of_objects 1
	number_of_groups 7
	set_scaling_to_player_number
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_CENTER
	temp_min_distance_group_placement 3
}

/* Boars */
create_object DLC_RHINO
{
	set_place_for_every_player
	number_of_groups 1
	set_gaia_object_only
	avoid_forest_zone 1
	terrain_to_place_on VAR_TERRAIN_CENTER
	actor_area 100
	actor_area_radius 1
	min_distance_to_players 15
	max_distance_to_players 17
	set_circular_placement
}
create_object DLC_ELEPHANT
{
	set_place_for_every_player
	number_of_groups 1
	set_gaia_object_only	
	avoid_forest_zone 1
	terrain_to_place_on VAR_TERRAIN_CENTER
	avoid_actor_area 100
	min_distance_to_players 15
	max_distance_to_players 17
	set_circular_placement
}
	
/* Other Stragglers */
create_object VAR_OBJECT_STRAGGLERTREE
{
	number_of_groups 500
	set_scale_by_groups
	min_distance_to_players 12
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_BASE
	temp_min_distance_group_placement 8
	avoid_all_actor_areas
}

/* Other Stragglers */
create_object VAR_OBJECT_STRAGGLERTREE
{
	number_of_groups 500
	set_scale_by_groups
	min_distance_to_players 12
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_CENTER
	temp_min_distance_group_placement 5
	avoid_all_actor_areas
}

/* Fish */
create_object GREAT_FISH_MARLIN /* Deep ones */
{
	number_of_groups 256
	max_distance_to_other_zones 7
	terrain_to_place_on VAR_TERRAIN_WATER_DEEP
	
	if TINY_MAP
		temp_min_distance_group_placement 6
	elseif SMALL_MAP
		temp_min_distance_group_placement 7
	elseif MEDIUM_MAP
		temp_min_distance_group_placement 8
	elseif LARGE_MAP
		temp_min_distance_group_placement 9
	else
		temp_min_distance_group_placement 10
	endif
}
create_object SHORE_FISH /* Shore ones */
{
	number_of_groups 256
	terrain_to_place_on WATER
	temp_min_distance_group_placement 6
}

/* Extra Golds */
create_object GOLD
{
	min_distance_to_players 25
	number_of_objects 2
	number_of_groups 2
	set_scaling_to_player_number
	temp_min_distance_group_placement 9
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 10
	actor_area_radius 5
	avoid_all_actor_areas
	terrain_to_place_on VAR_TERRAIN_BASE
}
create_object GOLD
{
	min_distance_to_players 25
	number_of_objects 2
	number_of_groups 2
	set_scaling_to_player_number
	temp_min_distance_group_placement 9
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 10
	actor_area_radius 5
	avoid_all_actor_areas
	terrain_to_place_on VAR_TERRAIN_CENTER
}

/* Extra Stones */
create_object STONE
{
	min_distance_to_players 25
	number_of_objects 2
	number_of_groups 2
	set_scaling_to_player_number
	temp_min_distance_group_placement 9
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 11
	actor_area_radius 5
	avoid_all_actor_areas
	terrain_to_place_on VAR_TERRAIN_BASE
}

/* Relics */
create_object RELIC
{
	terrain_to_place_on VAR_TERRAIN_CENTER
	min_distance_to_players 20
	number_of_objects 1
	avoid_forest_zone 3
	find_closest_to_map_center
	actor_area 99
	actor_area_radius 30
	min_distance_to_map_edge 10
}
create_object RELIC
{
	terrain_to_place_on VAR_TERRAIN_BASE
	min_distance_to_players 40
	number_of_objects 1
	avoid_forest_zone 3
	actor_area 98
	actor_area_radius 30
	avoid_actor_area 99
	min_distance_to_map_edge 10
}
create_object RELIC
{
	terrain_to_place_on VAR_TERRAIN_BASE
	min_distance_to_players 40
	number_of_objects 1
	avoid_forest_zone 3
	temp_min_distance_group_placement 50
	actor_area 97
	actor_area_radius 30
	avoid_actor_area 99
	avoid_actor_area 98
	min_distance_to_map_edge 10
}
create_object RELIC
{
	terrain_to_place_on VAR_TERRAIN_BASE
	min_distance_to_players 20
	max_distance_to_players 30
	number_of_objects 1
	number_of_groups 1
	set_place_for_every_player
	set_gaia_object_only
	avoid_forest_zone 3
	temp_min_distance_group_placement 25
	avoid_actor_area 99
	avoid_actor_area 98
	avoid_actor_area 97
	min_distance_to_map_edge 10
}

/* King for regicide */
if VAR_GAMEMODE_REG
	create_object KING
	{
		number_of_objects 1
		set_place_for_every_player
		min_distance_to_players 7
		max_distance_to_players 9
	}
endif
