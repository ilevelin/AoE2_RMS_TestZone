/* === Golden Canyon (v1.1) ===
MADE BY ILEVELIN
IDEA BY RALBER

Supported players/teams configuration:
	Any 2 team configuration with a limit of 4 players in a single team
	
Supported map sizes:
	2p
	3p
	4p
	6p
	8p
	
Supported game modes:
	ANY except Empire Wars
	
NOTE: 
	Playing with any non-supported player configuration, map size and/or game mode may place
	the players in unbalanced positions, bug the terrain generation or even not load. 
	Support to some other configurations MAY be introduced in the future.
	
Scripting references:
	https://docs.aoe2map.net/
	http://userpatch.aiscripters.net/reference.html# 
	https://docs.google.com/document/d/1eXRO5ZoSwrVn_rwyVx4k3FhAmqBwRVVzs95OPyIOrd0/edit
	https://www.forgottenempires.net/age-of-empires-ii-definitive-edition/rms-features
	
Changelog: [ +:Added | -:Removed | *:Adjusted | !:Fixed ]
	( v1.1 ) 2022/06/03
		* Reduced the zone where players can spawn when solo in the team to remove the posibility of spawning too close to the corner, making it too easy to wall off.
		! Removed terrain restriction for sheeps, allowing them to spawn in the middle layer. 
			> This should fix all the map generations where one or both pairs of extra sheeps fail to spawn due to space restrictions caused by forests.
	( v1.0 ) 2022/01/14
		+ Added relics, as it was intended.
			> 1 relic in middle layer per player
			> 3 relics in lower/center layer
	( v0.3 ) 2021/12/30
		- Further reduced both gold and stone on the map. Exact values are as follow (Asuming 2 players):
			> Secondary Gold
				Resource count per vein: 800 > 600 [-200]
				Group count: 20 > 8 [-12]
			> Secondary Stone
				Group count: 10 > 6 [-4]
			> Central Gold
				Resource count per vein: 650 > 500 [-150]
				Group count: 8 > 10 [+2]
			> Central Stone
				Vein count per group: 4 > 3 [-1]
				Group count: 5 > 6 [+1]
		* Changed resource scalling from map size to player count
		* Told animals to stay away from forests at least 3 tiles (sheep, rhinos and elephants)
		* Balanced forest spawning so both sides have the exact same amount of wood in the high/player layers
	( v0.2 ) 2021/05/18
		+ Added little hills to mid and high/player layers
		- Reduced both upper/player layer and lower/center layer size in larger maps.
		- Drastically reduced both gold and stone on the map. Exact values are as follow:
			> Secondary Gold
				Vein count per group: 4 > 3 [-1]
				Group count: 30 > 20 [-10] (in a 100x100 map)
			> Secondary Stone
				Vein count per group: 4 > 2 [-2]
				Group count: 20 > 10 [-10] (in a 100x100 map)
			> Central Gold
				Resource count per vein: 800 > 650 [-150]
				Vein count per group: 7 > 5 [-2]
				Group count: 9 > 8 [-1] (in a 100x100 map)
			> Central Stone
				Resource count per vein: 350 > 300 [-50]
				Vein count per group: 5 > 4 [-1]
				Group count: 6 > 5 [-1] (in a 100x100 map)
	( v0.1 ) 2020/10/26
		+ Hello World!
*/
 
#const VAR_TERRAIN_PLAYER 3 /* DIRT3 */
#const VAR_TERRAIN_HIGH 11 /* DIRT2 */
#const VAR_TERRAIN_MEDIUM 6 /* DIRT */
#const VAR_TERRAIN_DEEP 45 /* DLC_CRACKED */
#const VAR_TERRAIN_CLIFF 14 /* DESERT */
#const VAR_TERRAIN_FOREST 48 /* DLC_DRAGONFOREST */

#const VAR_TERRAIN_PLACEHOLDER1 2 /* BEACH */
#const VAR_TERRAIN_PLACEHOLDER2 53 /* DLC_BEACH4 */
 
#const VAR_OBJECT_STRAGGLERTREE 1051 /* DLC_DRAGONTREE */
 
/* Which team goes where */
start_random
percent_chance 50
	#define VAR_TEAMPLACEMENT_TEAMSIDE
end_random

/* Team placement order */
start_random
percent_chance 50
	#define VAR_TEAMPLACEMENT_TEAM1ORDER
end_random

start_random
percent_chance 50
	#define VAR_TEAMPLACEMENT_TEAM2ORDER
end_random

<PLAYER_SETUP>
direct_placement

<LAND_GENERATION>
base_terrain VAR_TERRAIN_CLIFF

create_land
{
	terrain_type VAR_TERRAIN_PLACEHOLDER1
	base_elevation 4
	
	left_border 0
	right_border 0
	top_border 0
	if TINY_MAP
		bottom_border 76
	elseif SMALL_MAP
		bottom_border 78
	elseif MEDIUM_MAP
		bottom_border 80
	elseif LARGE_MAP
		bottom_border 82
	else
		bottom_border 84
	endif
}
create_land
{
	terrain_type VAR_TERRAIN_PLACEHOLDER1
	base_elevation 4
	
	left_border 0
	right_border 0
	bottom_border 0
	if TINY_MAP
		top_border 76
	elseif SMALL_MAP
		top_border 78
	elseif MEDIUM_MAP
		top_border 80
	elseif LARGE_MAP
		top_border 82
	else
		top_border 84
	endif
}

create_land
{
	terrain_type VAR_TERRAIN_MEDIUM
	base_elevation 2
	
	left_border 0
	right_border 0
	if TINY_MAP
		top_border 62
		bottom_border 26
	elseif SMALL_MAP
		top_border 61
		bottom_border 24
	elseif MEDIUM_MAP
		top_border 60
		bottom_border 22
	elseif LARGE_MAP
		top_border 59
		bottom_border 20
	else
		top_border 58
		bottom_border 18
	endif
}
create_land
{
	terrain_type VAR_TERRAIN_MEDIUM
	base_elevation 2
	
	left_border 0
	right_border 0
	if TINY_MAP
		top_border 26
		bottom_border 62
	elseif SMALL_MAP
		top_border 24
		bottom_border 61
	elseif MEDIUM_MAP
		top_border 22
		bottom_border 60
	elseif LARGE_MAP
		top_border 20
		bottom_border 59
	else
		top_border 18
		bottom_border 58
	endif
}
/* PLAYERS */
if 2_PLAYER_GAME
	create_land /* T1 */
	{
		terrain_type VAR_TERRAIN_PLAYER
		base_elevation 4
		border_fuzziness 100
		clumping_factor 100
		number_of_tiles 100
		
		if VAR_TEAMPLACEMENT_TEAMSIDE
			land_position rnd(30,70) 10
		else 
			land_position rnd(30,70) 90
		endif
		assign_to_player 1
	}
	create_land /* T2 */
	{
		terrain_type VAR_TERRAIN_PLAYER
		base_elevation 4
		border_fuzziness 100
		clumping_factor 100
		number_of_tiles 100
		
		if VAR_TEAMPLACEMENT_TEAMSIDE
			land_position rnd(30,70) 90
		else 
			land_position rnd(30,70) 10
		endif
		assign_to_player 2
	}
else
	if TEAM1_SIZE2
		if VAR_TEAMPLACEMENT_TEAM1ORDER
			create_land /* T1-B */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(20,45) 10
				else 
					land_position rnd(20,45) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-T */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,80) 10
				else 
					land_position rnd(55,80) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		else
			create_land /* T1-T */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,80) 10
				else 
					land_position rnd(55,80) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-B */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(20,45) 10
				else 
					land_position rnd(20,45) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		endif
	elseif TEAM1_SIZE3
		if VAR_TEAMPLACEMENT_TEAM1ORDER
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(10,30) 10
				else 
					land_position rnd(10,30) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-P */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(40,60) 10
				else 
					land_position rnd(40,60) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(70,90) 10
				else 
					land_position rnd(70,90) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		else
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(70,90) 10
				else 
					land_position rnd(70,90) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-P */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(40,60) 10
				else 
					land_position rnd(40,60) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(10,30) 10
				else 
					land_position rnd(10,30) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		endif
	elseif TEAM1_SIZE4
		if VAR_TEAMPLACEMENT_TEAM1ORDER
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(5,20) 10
				else 
					land_position rnd(5,20) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(30,45) 10
				else 
					land_position rnd(30,45) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,70) 10
				else 
					land_position rnd(55,70) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(80,95) 10
				else 
					land_position rnd(80,95) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		else
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(80,95) 10
				else 
					land_position rnd(80,95) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,70) 10
				else 
					land_position rnd(55,70) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(30,45) 10
				else 
					land_position rnd(30,45) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(5,20) 10
				else 
					land_position rnd(5,20) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		endif
	else
		create_land /* T1 */
		{
			terrain_type VAR_TERRAIN_PLAYER
			base_elevation 4
			border_fuzziness 100
			clumping_factor 100
			number_of_tiles 100
		
			if VAR_TEAMPLACEMENT_TEAMSIDE
				land_position rnd(20,80) 10
			else 
				land_position rnd(20,80) 90
			endif
			assign_to AT_TEAM 0 0 0
		}
	endif

	if TEAM2_SIZE2
		if VAR_TEAMPLACEMENT_TEAM2ORDER
			create_land /* T2-B */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(20,45) 90
				else 
					land_position rnd(20,45) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-T */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,80) 90
				else 
					land_position rnd(55,80) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		else
			create_land /* T2-T */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,80) 90
				else 
					land_position rnd(55,80) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-B */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(20,45) 90
				else 
					land_position rnd(20,45) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		endif
	elseif TEAM2_SIZE3
		if VAR_TEAMPLACEMENT_TEAM2ORDER
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(10,30) 90
				else 
					land_position rnd(10,30) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-P */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(40,60) 90
				else 
					land_position rnd(40,60) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(70,90) 90
				else 
					land_position rnd(70,90) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		else
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(70,90) 90
				else 
					land_position rnd(70,90) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-P */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(40,60) 90
				else 
					land_position rnd(40,60) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(10,30) 90
				else 
					land_position rnd(10,30) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		endif
	elseif TEAM2_SIZE4
		if VAR_TEAMPLACEMENT_TEAM2ORDER
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(5,20) 90
				else 
					land_position rnd(5,20) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(30,45) 90
				else 
					land_position rnd(30,45) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,70) 90
				else 
					land_position rnd(55,70) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(80,95) 90
				else 
					land_position rnd(80,95) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		else
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(80,95) 90
				else 
					land_position rnd(80,95) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,70) 90
				else 
					land_position rnd(55,70) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(30,45) 90
				else 
					land_position rnd(30,45) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 100
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(5,20) 90
				else 
					land_position rnd(5,20) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		endif
	else
		create_land /* T2 */
		{
			terrain_type VAR_TERRAIN_PLAYER
			base_elevation 4
			border_fuzziness 100
			clumping_factor 100
			number_of_tiles 100
		
			if VAR_TEAMPLACEMENT_TEAMSIDE
				land_position 18 52
			else 
				land_position 82 52
			endif
			assign_to AT_TEAM 0 0 0
		}
	endif
endif


<ELEVATION_GENERATION>
enable_balanced_elevation

create_elevation 3
{
	base_terrain VAR_TERRAIN_MEDIUM
	number_of_tiles 800
	number_of_clumps 80
	set_scale_by_size
}

create_elevation 5
{
	base_terrain VAR_TERRAIN_HIGH
	number_of_tiles 500
	number_of_clumps 50
	set_scale_by_size
}


<TERRAIN_GENERATION>

create_terrain VAR_TERRAIN_DEEP 
{
	base_terrain VAR_TERRAIN_CLIFF
	land_percent 100
	height_limits 0 0
}

create_terrain VAR_TERRAIN_PLACEHOLDER2 /* Selecting one team's zone */
{
	base_terrain VAR_TERRAIN_PLACEHOLDER1
	land_percent 100
	number_of_clumps 1
}

create_terrain VAR_TERRAIN_FOREST /* Spawinig that team's woodlines */
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	if TINY_MAP
		number_of_tiles 500
		number_of_clumps 10
	elseif SMALL_MAP
		number_of_tiles 750
		number_of_clumps 15
	elseif MEDIUM_MAP
		number_of_tiles 1000
		number_of_clumps 20
	elseif LARGE_MAP
		number_of_tiles 1250
		number_of_clumps 25
	else
		number_of_tiles 1500
		number_of_clumps 30
	endif
	clumping_factor 35
	set_avoid_player_start_areas
}

create_terrain VAR_TERRAIN_FOREST /* Spawinig the other team's woodlines */
{
	base_terrain VAR_TERRAIN_PLACEHOLDER1
	if TINY_MAP
		number_of_tiles 500
		number_of_clumps 10
	elseif SMALL_MAP
		number_of_tiles 750
		number_of_clumps 15
	elseif MEDIUM_MAP
		number_of_tiles 1000
		number_of_clumps 20
	elseif LARGE_MAP
		number_of_tiles 1250
		number_of_clumps 25
	else
		number_of_tiles 1500
		number_of_clumps 30
	endif
	clumping_factor 35
	set_avoid_player_start_areas
}

create_terrain VAR_TERRAIN_HIGH /* Removing placeholder terrain 1 */
{
	base_terrain VAR_TERRAIN_PLACEHOLDER1
	land_percent 100
	number_of_clumps 999
}

create_terrain VAR_TERRAIN_HIGH /* Removing placeholder terrain 2 */
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	land_percent 100
	number_of_clumps 999
}

create_terrain VAR_TERRAIN_FOREST /* Mid-zone woodies */
{
	base_terrain VAR_TERRAIN_MEDIUM
	if TINY_MAP
		number_of_tiles 200
		number_of_clumps 20
	elseif SMALL_MAP
		number_of_tiles 400
		number_of_clumps 40
	elseif MEDIUM_MAP
		number_of_tiles 600
		number_of_clumps 60
	elseif LARGE_MAP
		number_of_tiles 800
		number_of_clumps 80
	else
		number_of_tiles 1000
		number_of_clumps 100
	endif
	clumping_factor 35
	set_avoid_player_start_areas
}
/*
<CONNECTION_GENERATION>
*/

<OBJECTS_GENERATION> 
/* Player Things */
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object SCOUT
{
	number_of_objects 1
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}
create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 6
}

if DEATH_MATCH
elseif EMPIRE_WARS
else
	/* Sheeps */
	create_object SHEEP /* Close ones */
	{
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 8
		find_closest
		number_of_objects 4
		set_gaia_object_only
		set_tight_grouping
		avoid_forest_zone 3
	}
	create_object SHEEP /* Further ones */
	{
		set_place_for_every_player
		min_distance_to_players 20
		max_distance_to_players 25
		number_of_objects 2
		number_of_groups 2
		temp_min_distance_group_placement 10
		set_gaia_object_only
		set_tight_grouping
		avoid_forest_zone 3
	}

	/* Bushes */
	create_object FORAGE_BUSH
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 12
		number_of_objects 6
		set_gaia_object_only
		set_tight_grouping
		avoid_forest_zone 2
		actor_area 1
		actor_area_radius 5
		avoid_all_actor_areas
	}

	/* Boars */
	create_object DLC_RHINO
	{
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		number_of_groups 1
		set_gaia_object_only
		avoid_forest_zone 1
		terrain_to_place_on VAR_TERRAIN_HIGH
		avoid_forest_zone 3
	}
	create_object DLC_ELEPHANT
	{
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		number_of_groups 1
		set_gaia_object_only
		avoid_forest_zone 1
		terrain_to_place_on VAR_TERRAIN_HIGH
		avoid_forest_zone 3
	}
	
	/* Deers */
	create_object DLC_IBEX
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 15
		number_of_objects 3
		set_tight_grouping
		set_gaia_object_only
		terrain_to_place_on VAR_TERRAIN_HIGH
		avoid_forest_zone 3
	}
	
	/* Player Stragglers */
	create_object VAR_OBJECT_STRAGGLERTREE
	{
		number_of_groups 6
		set_place_for_every_player
		min_distance_to_players 2
		max_distance_to_players 8
		set_gaia_object_only
		terrain_to_place_on VAR_TERRAIN_HIGH
		temp_min_distance_group_placement 3
	}
endif

/* Main Golds */
create_object GOLD
{
	terrain_to_place_on VAR_TERRAIN_HIGH
	
	set_place_for_every_player
	min_distance_to_players 8
	max_distance_to_players 12
	
	number_of_objects 2
	number_of_groups 1
	temp_min_distance_group_placement 18
	
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	avoid_cliff_zone 5
	
	avoid_actor_area 1
	actor_area_radius 1
	actor_area 2
}

/* Main Stone */
create_object STONE
{
	terrain_to_place_on VAR_TERRAIN_HIGH
	
	set_place_for_every_player
	min_distance_to_players 8
	max_distance_to_players 12
	
	number_of_objects 1
	number_of_groups 1
	temp_min_distance_group_placement 18
	set_tight_grouping
	
	set_gaia_object_only
	avoid_forest_zone 2
	avoid_cliff_zone 5
	
	actor_area_to_place_in 2
}

/* Secondary Gold */
create_object GOLD
{
	terrain_to_place_on VAR_TERRAIN_MEDIUM
	
	number_of_objects 3
	number_of_groups 4
	set_scaling_to_player_number
	temp_min_distance_group_placement 25
	set_tight_grouping
	resource_delta -200
	
	set_gaia_object_only
	avoid_forest_zone 2
	avoid_cliff_zone 5
	
	actor_area 20
	actor_area_radius 10
}

/* Secondary Stone */
create_object STONE
{
	terrain_to_place_on VAR_TERRAIN_MEDIUM
	
	number_of_objects 2
	number_of_groups 3
	set_scaling_to_player_number
	temp_min_distance_group_placement 35
	set_tight_grouping
	
	set_gaia_object_only
	avoid_forest_zone 2
	avoid_cliff_zone 5
	
	actor_area 21
	actor_area_radius 10
}

/* Center Gold */
create_object GOLD
{
	terrain_to_place_on VAR_TERRAIN_DEEP
	number_of_objects 5
	number_of_groups 5
	resource_delta -300
	temp_min_distance_group_placement 10
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 30
	actor_area_radius 2
	avoid_all_actor_areas
	set_scaling_to_player_number
	avoid_cliff_zone 5
}

/* Center Stone */
create_object STONE
{
	terrain_to_place_on VAR_TERRAIN_DEEP
	number_of_objects 3
	number_of_groups 3
	resource_delta -50
	temp_min_distance_group_placement 10
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 31
	actor_area_radius 2
	avoid_all_actor_areas
	set_scaling_to_player_number
	avoid_cliff_zone 5
}

/* Extra Deers */
create_object DLC_IBEX
{
	min_distance_to_players 30
	number_of_objects 2
	number_of_groups 10
	temp_min_distance_group_placement 15
	set_scaling_to_map_size
	set_tight_grouping
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_HIGH
}

/* Extra Bushes */
create_object FORAGE_BUSH
{
	min_distance_to_players 30
	number_of_objects 3
	number_of_groups 4
	set_scaling_to_map_size
	temp_min_distance_group_placement 20
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 1
	actor_area_radius 5
	avoid_all_actor_areas
	terrain_to_place_on VAR_TERRAIN_HIGH
}

/* Relic nearby every player */
create_object RELIC
{
	min_distance_to_players 20
	max_distance_to_players 30
	terrain_to_place_on VAR_TERRAIN_MEDIUM
	number_of_objects 1
	number_of_groups 1
	set_gaia_object_only
	set_place_for_every_player
}

/* 3 Relics in the middle zone */
create_object RELIC
{
	temp_min_distance_group_placement 25
	terrain_to_place_on VAR_TERRAIN_DEEP
	number_of_objects 1
	number_of_groups 3
	set_gaia_object_only
}

/* King and castle for regicide */
if REGICIDE
	create_object KING
	{
		number_of_objects 1
		set_place_for_every_player
		min_distance_to_players 7
		max_distance_to_players 9
	}
	create_object CASTLE
	{
		number_of_objects 1
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		avoid_all_actor_areas
	}
endif
