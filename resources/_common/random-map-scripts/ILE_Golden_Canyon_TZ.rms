/* === Golden Canyon (v1.5) ===
MADE BY ILEVELIN
IDEA BY RALBER

Supported players/teams configuration:
	Any 2 team configuration with a limit of 4 players in a single team
	
Supported map sizes:
	2p
	3p
	4p
	6p
	8p
	
Supported game modes:
	ANY
	
NOTE: 
	Playing with any non-supported player configuration, map size and/or game mode may place
	the players in unbalanced positions, bug the terrain generation or even not load. 
	Support to some other configurations MAY be introduced in the future.
	
Scripting references:
	https://docs.aoe2map.net/
	
Changelog: [ +:Added | -:Removed | *:Adjusted | !:Fixed ]
	( v1.5 ) 2023/03/26
		* Increased player layer on larger map sizes
	( v1.4 ) 2022/12/16
		+ Made one of the main sheep automatically captured
		! Placeholder terrains replaced with non blocking ones
	( v1.3 ) 2022/09/04
		* Replaced the stone deposit that appears next to the main gold to another gold deposit in EW as sometimes villagers would mine it instead of gold and the stone ammount is irrelevant for this game mode
		* Changed forest generation so the players have 3 close but small forests and the rest big ones further away
			> This change is mainly for EW, but also to have more fair forest spawns, as sometimes one player had forests too far away and thus hard to defend
	( v1.2 ) 2022/09/04
		+ Added support for Empire Wars
		* Now forests will spawn further away from the players
		! Fixed that sometimes a beach terrain would spawn next to a forest if it was too close to the player or the ramp to the medium zone
			> It is a known bug that sometimes that terrain still spawns when the forest is too close to a map border and hollow in that side
	( v1.1 ) 2022/06/03
		* Reduced the zone where players can spawn when solo in the team to remove the posibility of spawning too close to the corner, making it too easy to wall off
		! Removed terrain restriction for sheeps, allowing them to spawn in the middle layer. 
			> This should fix all the map generations where one or both pairs of extra sheeps fail to spawn due to space restrictions caused by forests.
	( v1.0 ) 2022/01/14
		+ Added relics, as it was intended.
			> 1 relic in middle layer per player
			> 3 relics in lower/center layer
	( v0.3 ) 2021/12/30
		- Further reduced both gold and stone on the map. Exact values are as follow (Asuming 2 players):
			> Secondary Gold
				Resource count per vein: 800 > 600 [-200]
				Group count: 20 > 8 [-12]
			> Secondary Stone
				Group count: 10 > 6 [-4]
			> Central Gold
				Resource count per vein: 650 > 500 [-150]
				Group count: 8 > 10 [+2]
			> Central Stone
				Vein count per group: 4 > 3 [-1]
				Group count: 5 > 6 [+1]
		* Changed resource scalling from map size to player count
		* Told animals to stay away from forests at least 3 tiles (sheep, rhinos and elephants)
		* Balanced forest spawning so both sides have the exact same amount of wood in the high/player layers
	( v0.2 ) 2021/05/18
		+ Added little hills to mid and high/player layers
		- Reduced both upper/player layer and lower/center layer size in larger maps.
		- Drastically reduced both gold and stone on the map. Exact values are as follow:
			> Secondary Gold
				Vein count per group: 4 > 3 [-1]
				Group count: 30 > 20 [-10] (in a 100x100 map)
			> Secondary Stone
				Vein count per group: 4 > 2 [-2]
				Group count: 20 > 10 [-10] (in a 100x100 map)
			> Central Gold
				Resource count per vein: 800 > 650 [-150]
				Vein count per group: 7 > 5 [-2]
				Group count: 9 > 8 [-1] (in a 100x100 map)
			> Central Stone
				Resource count per vein: 350 > 300 [-50]
				Vein count per group: 5 > 4 [-1]
				Group count: 6 > 5 [-1] (in a 100x100 map)
	( v0.1 ) 2020/10/26
		+ Hello World!
*/

/* *** DEBUG CONFIGS *** */
/* #define VAR_DEBUG_FORCE_RM */
/* #define VAR_DEBUG_FORCE_EW */
/* #define VAR_DEBUG_FORCE_DM */

/* #define VAR_DEBUG_FORCE_REG */

/* TERRAINS */
#const VAR_TERRAIN_PLAYER 11 /* DIRT2 */
#const VAR_TERRAIN_HIGH 11 /* DIRT2 */
#const VAR_TERRAIN_MEDIUM 41 /* DLC_SAVANNAH */
#const VAR_TERRAIN_DEEP 45 /* DLC_CRACKED */
#const VAR_TERRAIN_CLIFF 14 /* DESERT */
#const VAR_TERRAIN_FOREST 48 /* DLC_DRAGONFOREST */

#const VAR_TERRAIN_PLACEHOLDER1 3 /* DIRT3 */
#const VAR_TERRAIN_PLACEHOLDER2 42 /* DLC_DIRT4 */
#const VAR_TERRAIN_PLACEHOLDER3 6 /* DIRT */
 
/* OBJECTS */
#const VAR_OBJECT_STRAGGLERTREE 1051 /* DLC_DRAGONTREE */

#const VAR_OBJECT_PLACEHOLDER_SMALL 600 /* Flag A */
effect_amount SET_ATTRIBUTE VAR_OBJECT_PLACEHOLDER_SMALL ATTR_HITPOINTS 0

#const VAR_OBJECT_PLACEHOLDER_BIG 1376 /* Torch B */
effect_amount SET_ATTRIBUTE VAR_OBJECT_PLACEHOLDER_BIG ATTR_HITPOINTS 0

#const VAR_OBJECT_DOCK_BLOCKER 1654
effect_percent GAIA_SET_ATTRIBUTE VAR_OBJECT_DOCK_BLOCKER ATTR_RADIUS_1 50
effect_percent GAIA_SET_ATTRIBUTE VAR_OBJECT_DOCK_BLOCKER ATTR_RADIUS_2 50

#const VAR_OBJECT_LAZY_MALE 1285 /* FE Flag */
effect_amount SET_ATTRIBUTE VAR_OBJECT_LAZY_MALE ATTR_DEAD_ID 83
effect_amount SET_ATTRIBUTE VAR_OBJECT_LAZY_MALE ATTR_HITPOINTS 0

#const VAR_OBJECT_LAZY_FEMALE 851 /* ES Flag */
effect_amount SET_ATTRIBUTE VAR_OBJECT_LAZY_FEMALE ATTR_DEAD_ID 293
effect_amount SET_ATTRIBUTE VAR_OBJECT_LAZY_FEMALE ATTR_HITPOINTS 0
 
/* GAMEMODE DETECTION */
if VAR_DEBUG_FORCE_RM
elseif VAR_DEBUG_FORCE_EW
	#define VAR_GAMEMODE_EW
elseif VAR_DEBUG_FORCE_DM
	#define VAR_GAMEMODE_DM
elseif EMPIRE_WARS
	#define VAR_GAMEMODE_EW
elseif DEATH_MATCH
	#define VAR_GAMEMODE_DM
endif

if VAR_DEBUG_FORCE_REG
	#define VAR_GAMEMODE_REG
elseif REGICIDE
	#define VAR_GAMEMODE_REG
endif

/* Which team goes where */
start_random
percent_chance 50
	#define VAR_TEAMPLACEMENT_TEAMSIDE
end_random

/* Team placement order */
start_random
percent_chance 50
	#define VAR_TEAMPLACEMENT_TEAM1ORDER
end_random

start_random
percent_chance 50
	#define VAR_TEAMPLACEMENT_TEAM2ORDER
end_random

<PLAYER_SETUP>
direct_placement

<LAND_GENERATION>
base_terrain VAR_TERRAIN_CLIFF

create_land
{
	terrain_type VAR_TERRAIN_PLACEHOLDER1
	land_percent 100
	base_elevation 4
	
	left_border 0
	right_border 0
	top_border 0
	bottom_border 76
}

create_land
{
	terrain_type VAR_TERRAIN_PLACEHOLDER1
	land_percent 100
	base_elevation 4
	
	left_border 0
	right_border 0
	bottom_border 0
	top_border 76
}

create_land
{
	terrain_type VAR_TERRAIN_MEDIUM
	land_percent 100
	base_elevation 2
	
	left_border 0
	right_border 0
	bottom_border 26
	if TINY_MAP
		top_border 62
	elseif SMALL_MAP
		top_border 61
	elseif MEDIUM_MAP
		top_border 60
	elseif LARGE_MAP
		top_border 59
	else
		top_border 58
	endif
}
create_land
{
	terrain_type VAR_TERRAIN_MEDIUM
	land_percent 100
	base_elevation 2
	
	left_border 0
	right_border 0
	top_border 26
	if TINY_MAP
		bottom_border 62
	elseif SMALL_MAP
		bottom_border 61
	elseif MEDIUM_MAP
		bottom_border 60
	elseif LARGE_MAP
		bottom_border 59
	else
		bottom_border 58
	endif
}
/* PLAYERS */
if 2_PLAYER_GAME
	create_land /* T1 */
	{
		terrain_type VAR_TERRAIN_PLAYER
		base_elevation 4
		border_fuzziness 100
		clumping_factor 100
		number_of_tiles 10
		
		if VAR_TEAMPLACEMENT_TEAMSIDE
			land_position rnd(30,70) 10
		else 
			land_position rnd(30,70) 90
		endif
		assign_to_player 1
	}
	create_land /* T2 */
	{
		terrain_type VAR_TERRAIN_PLAYER
		base_elevation 4
		border_fuzziness 100
		clumping_factor 100
		number_of_tiles 10
		
		if VAR_TEAMPLACEMENT_TEAMSIDE
			land_position rnd(30,70) 90
		else 
			land_position rnd(30,70) 10
		endif
		assign_to_player 2
	}
else
	if TEAM1_SIZE2
		if VAR_TEAMPLACEMENT_TEAM1ORDER
			create_land /* T1-B */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(20,45) 10
				else 
					land_position rnd(20,45) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-T */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,80) 10
				else 
					land_position rnd(55,80) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		else
			create_land /* T1-T */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,80) 10
				else 
					land_position rnd(55,80) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-B */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(20,45) 10
				else 
					land_position rnd(20,45) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		endif
	elseif TEAM1_SIZE3
		if VAR_TEAMPLACEMENT_TEAM1ORDER
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(10,30) 10
				else 
					land_position rnd(10,30) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-P */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(40,60) 10
				else 
					land_position rnd(40,60) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(70,90) 10
				else 
					land_position rnd(70,90) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		else
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(70,90) 10
				else 
					land_position rnd(70,90) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-P */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(40,60) 10
				else 
					land_position rnd(40,60) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(10,30) 10
				else 
					land_position rnd(10,30) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		endif
	elseif TEAM1_SIZE4
		if VAR_TEAMPLACEMENT_TEAM1ORDER
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(5,20) 10
				else 
					land_position rnd(5,20) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(30,45) 10
				else 
					land_position rnd(30,45) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,70) 10
				else 
					land_position rnd(55,70) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(80,95) 10
				else 
					land_position rnd(80,95) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		else
			create_land /* T1-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(80,95) 10
				else 
					land_position rnd(80,95) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-TP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,70) 10
				else 
					land_position rnd(55,70) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(30,45) 10
				else 
					land_position rnd(30,45) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
			create_land /* T1-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(5,20) 10
				else 
					land_position rnd(5,20) 90
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 1 -1 0
				else 
					assign_to AT_TEAM 1 0 0
				endif
			}
		endif
	else
		create_land /* T1 */
		{
			terrain_type VAR_TERRAIN_PLAYER
			base_elevation 4
			border_fuzziness 100
			clumping_factor 100
			number_of_tiles 10
		
			if VAR_TEAMPLACEMENT_TEAMSIDE
				land_position rnd(20,80) 10
			else 
				land_position rnd(20,80) 90
			endif
			assign_to AT_TEAM 0 0 0
		}
	endif

	if TEAM2_SIZE2
		if VAR_TEAMPLACEMENT_TEAM2ORDER
			create_land /* T2-B */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(20,45) 90
				else 
					land_position rnd(20,45) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-T */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,80) 90
				else 
					land_position rnd(55,80) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		else
			create_land /* T2-T */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,80) 90
				else 
					land_position rnd(55,80) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-B */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(20,45) 90
				else 
					land_position rnd(20,45) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		endif
	elseif TEAM2_SIZE3
		if VAR_TEAMPLACEMENT_TEAM2ORDER
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(10,30) 90
				else 
					land_position rnd(10,30) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-P */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(40,60) 90
				else 
					land_position rnd(40,60) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(70,90) 90
				else 
					land_position rnd(70,90) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		else
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(70,90) 90
				else 
					land_position rnd(70,90) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-P */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(40,60) 90
				else 
					land_position rnd(40,60) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(10,30) 90
				else 
					land_position rnd(10,30) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		endif
	elseif TEAM2_SIZE4
		if VAR_TEAMPLACEMENT_TEAM2ORDER
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(5,20) 90
				else 
					land_position rnd(5,20) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(30,45) 90
				else 
					land_position rnd(30,45) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,70) 90
				else 
					land_position rnd(55,70) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(80,95) 90
				else 
					land_position rnd(80,95) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		else
			create_land /* T2-TF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(80,95) 90
				else 
					land_position rnd(80,95) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-TP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(55,70) 90
				else 
					land_position rnd(55,70) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BP */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(30,45) 90
				else 
					land_position rnd(30,45) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
			create_land /* T2-BF */
			{
				terrain_type VAR_TERRAIN_PLAYER
				base_elevation 4
				border_fuzziness 100
				clumping_factor 100
				number_of_tiles 10
		
				if VAR_TEAMPLACEMENT_TEAMSIDE
					land_position rnd(5,20) 90
				else 
					land_position rnd(5,20) 10
				endif
				if TEAM_POSITIONS
					assign_to AT_TEAM 2 -1 0
				else 
					assign_to AT_TEAM 2 0 0
				endif
			}
		endif
	else
		create_land /* T2 */
		{
			terrain_type VAR_TERRAIN_PLAYER
			base_elevation 4
			border_fuzziness 100
			clumping_factor 100
			number_of_tiles 10
		
			if VAR_TEAMPLACEMENT_TEAMSIDE
				land_position 18 52
			else 
				land_position 82 52
			endif
			assign_to AT_TEAM 0 0 0
		}
	endif
endif


<ELEVATION_GENERATION>
enable_balanced_elevation

create_elevation 3
{
	base_terrain VAR_TERRAIN_MEDIUM
	number_of_tiles 800
	number_of_clumps 80
	set_scale_by_size
}

create_elevation 5
{
	base_terrain VAR_TERRAIN_HIGH
	number_of_tiles 500
	number_of_clumps 50
	set_scale_by_size
}


<TERRAIN_GENERATION>

create_terrain VAR_TERRAIN_DEEP 
{
	base_terrain VAR_TERRAIN_CLIFF
	land_percent 100
	height_limits 0 0
}
create_terrain VAR_TERRAIN_PLACEHOLDER2 /* Selecting one team's zone */
{
	base_terrain VAR_TERRAIN_PLACEHOLDER1
	land_percent 100
	number_of_clumps 1
}
create_terrain VAR_TERRAIN_PLACEHOLDER3 /* Placeholder terrain to evade player zones */
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	set_avoid_player_start_areas 20
	clumping_factor 35
	number_of_tiles 9999
	number_of_clumps 9
}
create_terrain VAR_TERRAIN_FOREST /* Spawinig that team's woodlines */
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	if TINY_MAP
		number_of_tiles 500
		number_of_clumps 10
	elseif SMALL_MAP
		number_of_tiles 750
		number_of_clumps 15
	elseif MEDIUM_MAP
		number_of_tiles 1000
		number_of_clumps 20
	elseif LARGE_MAP
		number_of_tiles 1250
		number_of_clumps 25
	else
		number_of_tiles 1500
		number_of_clumps 30
	endif
	clumping_factor 35
	set_avoid_player_start_areas 20
	spacing_to_other_terrain_types 1
}
create_terrain VAR_TERRAIN_HIGH /* Removing placeholder terrain */
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	land_percent 100
	number_of_clumps 99999
}
/* Repeating process for forests of the second team */
create_terrain VAR_TERRAIN_PLACEHOLDER2
{
	base_terrain VAR_TERRAIN_PLACEHOLDER1
	land_percent 100
	number_of_clumps 1
}
create_terrain VAR_TERRAIN_PLACEHOLDER3
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	set_avoid_player_start_areas 20
	clumping_factor 35
	number_of_tiles 9999
	number_of_clumps 9
}
create_terrain VAR_TERRAIN_FOREST
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	if TINY_MAP
		number_of_tiles 500
		number_of_clumps 10
	elseif SMALL_MAP
		number_of_tiles 750
		number_of_clumps 15
	elseif MEDIUM_MAP
		number_of_tiles 1000
		number_of_clumps 20
	elseif LARGE_MAP
		number_of_tiles 1250
		number_of_clumps 25
	else
		number_of_tiles 1500
		number_of_clumps 30
	endif
	clumping_factor 35
	set_avoid_player_start_areas 20
	spacing_to_other_terrain_types 1
}
create_terrain VAR_TERRAIN_HIGH
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	land_percent 100
	number_of_clumps 99999
}

create_terrain VAR_TERRAIN_PLACEHOLDER3 /* Selecting one players "close" zone */
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	clumping_factor 35
	land_percent 100
	number_of_clumps 1
}
create_terrain VAR_TERRAIN_FOREST /* Giving it 3 forests */
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	number_of_tiles 100
	number_of_clumps 3
	clumping_factor 35
	set_avoid_player_start_areas 13
}
create_terrain VAR_TERRAIN_HIGH /* Covering the placeholder */
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	land_percent 100
	number_of_clumps 99999
}
/* Same for player 2 */
create_terrain VAR_TERRAIN_PLACEHOLDER3 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	clumping_factor 35
	land_percent 100
	number_of_clumps 1
}
create_terrain VAR_TERRAIN_FOREST 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	number_of_tiles 100
	number_of_clumps 3
	clumping_factor 35
	set_avoid_player_start_areas 13
}
create_terrain VAR_TERRAIN_HIGH 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	land_percent 100
	number_of_clumps 99999
}
/* Same for player 3 */
create_terrain VAR_TERRAIN_PLACEHOLDER3 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	clumping_factor 35
	land_percent 100
	number_of_clumps 1
}
create_terrain VAR_TERRAIN_FOREST 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	number_of_tiles 100
	number_of_clumps 3
	clumping_factor 35
	set_avoid_player_start_areas 13
}
create_terrain VAR_TERRAIN_HIGH 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	land_percent 100
	number_of_clumps 99999
}
/* Same for player 4 */
create_terrain VAR_TERRAIN_PLACEHOLDER3 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	clumping_factor 35
	land_percent 100
	number_of_clumps 1
}
create_terrain VAR_TERRAIN_FOREST 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	number_of_tiles 100
	number_of_clumps 3
	clumping_factor 35
	set_avoid_player_start_areas 13
}
create_terrain VAR_TERRAIN_HIGH 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	land_percent 100
	number_of_clumps 99999
}
/* Same for player 5 */
create_terrain VAR_TERRAIN_PLACEHOLDER3 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	clumping_factor 35
	land_percent 100
	number_of_clumps 1
}
create_terrain VAR_TERRAIN_FOREST 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	number_of_tiles 100
	number_of_clumps 3
	clumping_factor 35
	set_avoid_player_start_areas 13
}
create_terrain VAR_TERRAIN_HIGH 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	land_percent 100
	number_of_clumps 99999
}
/* Same for player 6 */
create_terrain VAR_TERRAIN_PLACEHOLDER3 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	clumping_factor 35
	land_percent 100
	number_of_clumps 1
}
create_terrain VAR_TERRAIN_FOREST 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	number_of_tiles 100
	number_of_clumps 3
	clumping_factor 35
	set_avoid_player_start_areas 13
}
create_terrain VAR_TERRAIN_HIGH 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	land_percent 100
	number_of_clumps 99999
}
/* Same for player 7 */
create_terrain VAR_TERRAIN_PLACEHOLDER3 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	clumping_factor 35
	land_percent 100
	number_of_clumps 1
}
create_terrain VAR_TERRAIN_FOREST 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	number_of_tiles 100
	number_of_clumps 3
	clumping_factor 35
	set_avoid_player_start_areas 13
}
create_terrain VAR_TERRAIN_HIGH 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	land_percent 100
	number_of_clumps 99999
}
/* Same for player 8 */
create_terrain VAR_TERRAIN_PLACEHOLDER3 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	clumping_factor 35
	land_percent 100
	number_of_clumps 1
}
create_terrain VAR_TERRAIN_FOREST 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	number_of_tiles 100
	number_of_clumps 3
	clumping_factor 35
	set_avoid_player_start_areas 13
}
create_terrain VAR_TERRAIN_HIGH 
{
	base_terrain VAR_TERRAIN_PLACEHOLDER3
	land_percent 100
	number_of_clumps 99999
}
create_terrain VAR_TERRAIN_HIGH
{
	base_terrain VAR_TERRAIN_PLACEHOLDER2
	land_percent 100
	number_of_clumps 99999
}

create_terrain VAR_TERRAIN_FOREST /* Mid-zone woodies */
{
	base_terrain VAR_TERRAIN_MEDIUM
	if TINY_MAP
		number_of_tiles 200
		number_of_clumps 20
	elseif SMALL_MAP
		number_of_tiles 400
		number_of_clumps 40
	elseif MEDIUM_MAP
		number_of_tiles 600
		number_of_clumps 60
	elseif LARGE_MAP
		number_of_tiles 800
		number_of_clumps 80
	else
		number_of_tiles 1000
		number_of_clumps 100
	endif
	clumping_factor 35
	set_avoid_player_start_areas
}

/*
<CONNECTION_GENERATION>
*/

<OBJECTS_GENERATION> 
/* Player Things */
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object SCOUT
{
	number_of_objects 1
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}

/* Main Golds */
create_object GOLD
{
	terrain_to_place_on VAR_TERRAIN_HIGH
	
	set_place_for_every_player
	min_distance_to_players 8
	max_distance_to_players 12
	min_distance_to_map_edge 2
	avoid_forest_zone 2
	avoid_cliff_zone 5
	set_circular_placement
	
	number_of_objects 2
	number_of_groups 1
	temp_min_distance_group_placement 18
	
	set_gaia_object_only
	set_tight_grouping
	
	avoid_actor_area 1
	actor_area_radius 1
	actor_area 1300
}

/* Main Stone */
if VAR_GAMEMODE_EW
	create_object GOLD
else
	create_object STONE
endif
{
	terrain_to_place_on VAR_TERRAIN_HIGH
	
	set_place_for_every_player
	min_distance_to_players 8
	max_distance_to_players 12
	
	number_of_objects 1
	number_of_groups 1
	temp_min_distance_group_placement 18
	set_tight_grouping
	
	set_gaia_object_only
	avoid_forest_zone 2
	avoid_cliff_zone 5

	actor_area 9000
	actor_area_radius 7
	
	actor_area_to_place_in 1300
}

/* Secondary Gold */
create_object GOLD
{
	terrain_to_place_on VAR_TERRAIN_MEDIUM
	
	number_of_objects 3
	number_of_groups 4
	set_scaling_to_player_number
	temp_min_distance_group_placement 25
	set_tight_grouping
	resource_delta -200
	set_circular_placement
	
	set_gaia_object_only
	avoid_forest_zone 2
	avoid_cliff_zone 5
	
	actor_area 20
	actor_area_radius 10
}

/* Secondary Stone */
create_object STONE
{
	terrain_to_place_on VAR_TERRAIN_MEDIUM
	
	number_of_objects 2
	number_of_groups 3
	set_scaling_to_player_number
	temp_min_distance_group_placement 35
	set_tight_grouping
	set_circular_placement
	
	set_gaia_object_only
	avoid_forest_zone 2
	avoid_cliff_zone 5
	
	actor_area 21
	actor_area_radius 10
}

/* Center Gold */
create_object GOLD
{
	terrain_to_place_on VAR_TERRAIN_DEEP
	number_of_objects 5
	number_of_groups 5
	resource_delta -300
	temp_min_distance_group_placement 10
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 30
	actor_area_radius 2
	avoid_all_actor_areas
	set_scaling_to_player_number
	avoid_cliff_zone 5
}

/* Center Stone */
create_object STONE
{
	terrain_to_place_on VAR_TERRAIN_DEEP
	number_of_objects 3
	number_of_groups 3
	resource_delta -50
	temp_min_distance_group_placement 10
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	actor_area 31
	actor_area_radius 2
	avoid_all_actor_areas
	set_scaling_to_player_number
	avoid_cliff_zone 5
}

/* Extra Deers */
create_object DLC_IBEX
{
	min_distance_to_players 30
	number_of_objects 2
	number_of_groups 10
	temp_min_distance_group_placement 15
	set_scaling_to_map_size
	set_tight_grouping
	set_gaia_object_only
	terrain_to_place_on VAR_TERRAIN_HIGH
}

/* Extra Bushes */
create_object FORAGE_BUSH
{
	min_distance_to_players 30
	number_of_objects 3
	number_of_groups 4
	set_scaling_to_map_size
	temp_min_distance_group_placement 20
	set_gaia_object_only
	set_tight_grouping
	avoid_forest_zone 2
	avoid_all_actor_areas
	terrain_to_place_on VAR_TERRAIN_HIGH
}

/* Relic nearby every player */
create_object RELIC
{
	min_distance_to_players 20
	max_distance_to_players 30
	terrain_to_place_on VAR_TERRAIN_MEDIUM
	number_of_objects 1
	number_of_groups 1
	set_gaia_object_only
	set_place_for_every_player
}

/* 3 Relics in the middle zone */
create_object RELIC
{
	temp_min_distance_group_placement 25
	terrain_to_place_on VAR_TERRAIN_DEEP
	number_of_objects 1
	number_of_groups 3
	set_gaia_object_only
}

/* King and castle for regicide */
if VAR_GAMEMODE_REG
	create_object KING
	{
		number_of_objects 1
		set_place_for_every_player
		min_distance_to_players 7
		max_distance_to_players 9
		set_circular_placement
	}
	create_object CASTLE
	{
		number_of_objects 1
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		avoid_all_actor_areas
		set_circular_placement
	}
endif

if VAR_GAMEMODE_DM
else
	/* Deers */
	create_object DLC_IBEX
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 15
		number_of_objects 3
		set_tight_grouping
		set_gaia_object_only
		terrain_to_place_on VAR_TERRAIN_HIGH
		avoid_forest_zone 3
		set_circular_placement
	}

	/* Bushes */
	create_object FORAGE_BUSH
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 12
		number_of_objects 6
		set_gaia_object_only
		set_tight_grouping
		avoid_forest_zone 2
		actor_area 100
		actor_area_radius 1
		min_distance_to_map_edge 2
		avoid_actor_area 9000
		set_circular_placement
	}
endif

if VAR_GAMEMODE_DM
elseif VAR_GAMEMODE_EW
	/* Farms */
	create_object FARM
	{
		second_object VILLAGER
		set_place_for_every_player
		number_of_objects 2
		find_closest
	}
	/* Mill and foragers */
	create_object MILL
	{
		set_place_for_every_player
		actor_area_to_place_in 100
		actor_area 1100
		actor_area_radius 0
		find_closest
	}
	create_object VILLAGER
	{
		set_place_for_every_player
		actor_area_to_place_in 100
		avoid_actor_area 1100
		number_of_objects 4
		find_closest

		actor_area 1210
		actor_area_radius 3
	}
	/* Sheep */
	create_object VAR_OBJECT_PLACEHOLDER_BIG /* Placeholders to force sheep spawning position */
	{
		set_place_for_every_player
		set_gaia_object_only
		number_of_objects 5
		min_distance_to_players 1
		max_distance_to_players 1
		find_closest

		actor_area 1110
		actor_area_radius 0
	}
	create_object VAR_OBJECT_PLACEHOLDER_BIG /* THE sheep */
	{
		second_object SHEEP
		set_place_for_every_player
		min_distance_to_players 1
		max_distance_to_players 1
		number_of_objects 1
		find_closest

		actor_area 1111
		actor_area_radius 1

		avoid_actor_area 1110
	}
	create_object VAR_OBJECT_PLACEHOLDER_BIG /* Placeholder to avoid villagers spawning on top of the sheep */
	{
		set_place_for_every_player
		min_distance_to_players 1
		actor_area_to_place_in 1111
		avoid_actor_area 1110
		find_closest

		actor_area 1900
		actor_area_radius 0
		
	}
	create_object VAR_OBJECT_PLACEHOLDER_BIG /* 3 villagers, males */
	{
		second_object VAR_OBJECT_LAZY_MALE
		set_place_for_every_player
		min_distance_to_players 1
		number_of_objects 3
		actor_area_to_place_in 1111
		avoid_actor_area 1900
		avoid_actor_area 1901
		find_closest

		actor_area 1901
		actor_area_radius 0
	}
	create_object VAR_OBJECT_PLACEHOLDER_BIG /* 3 villagers, females */
	{
		second_object VAR_OBJECT_LAZY_FEMALE
		set_place_for_every_player
		min_distance_to_players 1
		number_of_objects 3
		actor_area_to_place_in 1111
		avoid_actor_area 1900
		avoid_actor_area 1901
		find_closest

		actor_area 1901
		actor_area_radius 0
	}
	create_object SHEEP /* The remaining sheep */
	{
		set_place_for_every_player
		min_distance_to_players 5
		max_distance_to_players 5
		number_of_objects 4
		set_tight_grouping

		avoid_actor_area 1100
	}
	/* Wood */
	create_object VAR_OBJECT_PLACEHOLDER_BIG /* First Lumbercamp */
	{
		second_object LUMBER_CAMP
		set_place_for_every_player
		min_distance_to_map_edge 2
		place_on_forest_zone
		avoid_actor_area 9001
		find_closest

		actor_area 1200
		actor_area_radius 3
	}
	create_object VILLAGER /* Villagers for first lumbercamp */
	{
		set_place_for_every_player
		place_on_forest_zone
		number_of_objects 4
		actor_area_to_place_in 1200
		avoid_actor_area 1210
		terrain_to_place_on VAR_TERRAIN_LAKE
		find_closest

		actor_area 1210
		actor_area_radius 3
	}

	create_object VAR_OBJECT_PLACEHOLDER_BIG /* Second Lumbercamp */
	{
		second_object LUMBER_CAMP
		set_place_for_every_player
		min_distance_to_map_edge 2
		place_on_forest_zone
		avoid_actor_area 1200
		avoid_actor_area 1210
		avoid_actor_area 9001
		find_closest

		actor_area 1201
		actor_area_radius 3
	}
	create_object VILLAGER /* Villagers for second lumbercamp */
	{
		set_place_for_every_player
		place_on_forest_zone
		number_of_objects 4
		actor_area_to_place_in 1201
		avoid_actor_area 1210
		terrain_to_place_on VAR_TERRAIN_LAKE
		find_closest

		actor_area 1210
		actor_area_radius 3
	}

	create_object VAR_OBJECT_PLACEHOLDER_BIG /* Third Lumbercamp */
	{
		second_object LUMBER_CAMP
		set_place_for_every_player
		min_distance_to_map_edge 2
		place_on_forest_zone
		avoid_actor_area 1200
		avoid_actor_area 1201
		avoid_actor_area 1210
		avoid_actor_area 9001
		find_closest

		actor_area 1202
		actor_area_radius 3
	}
	create_object VILLAGER /* Villagers for third lumbercamp */
	{
		set_place_for_every_player
		place_on_forest_zone
		number_of_objects 4
		actor_area_to_place_in 1202
		avoid_actor_area 1210
		terrain_to_place_on VAR_TERRAIN_LAKE
		find_closest

		actor_area 1210
		actor_area_radius 2
	}

	/* Gold */
	create_object MINING_CAMP
	{
		set_place_for_every_player
		actor_area_to_place_in 1300
		find_closest
		min_distance_to_map_edge 2
	}

	create_object VILLAGER
	{
		set_place_for_every_player
		actor_area_to_place_in 1300
		find_closest
		number_of_objects 4
		avoid_actor_area 1400
		avoid_forest_zone 1

		actor_area 1210
		actor_area_radius 3
	}

	/* Other buildings */
	create_object HOUSE
	{
		set_place_for_every_player
		min_distance_to_players 14
		max_distance_to_players 14
		number_of_objects 6
		find_closest_to_map_center
		avoid_actor_area 100
		avoid_actor_area 1210
	}
	create_object BARRACKS
	{
		set_place_for_every_player
		min_distance_to_players 12
		max_distance_to_players 14
		find_closest_to_map_center
		avoid_actor_area 100
		avoid_actor_area 1210
	}
	create_object BLACKSMITH
	{
		set_place_for_every_player
		min_distance_to_players 12
		max_distance_to_players 14
		find_closest_to_map_center
		avoid_actor_area 100
		avoid_actor_area 1210
	}
else
	create_object VILLAGER
	{
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 6
	}
	/* Sheeps */
	create_object SHEEP /* Close ones */
	{
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 8
		find_closest
		number_of_objects 3
		set_gaia_object_only
		set_tight_grouping
		avoid_forest_zone 3
		actor_area 999
		actor_area_radius 1
		set_circular_placement
	}
	create_object SHEEP
	{
		set_place_for_every_player
		find_closest
		avoid_forest_zone 3
		actor_area_to_place_in 999
	}
	create_object SHEEP /* Further ones */
	{
		set_place_for_every_player
		min_distance_to_players 20
		max_distance_to_players 25
		number_of_objects 2
		number_of_groups 2
		temp_min_distance_group_placement 10
		set_gaia_object_only
		set_tight_grouping
		avoid_forest_zone 3
		set_circular_placement
	}

	/* Boars */
	create_object DLC_RHINO
	{
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		number_of_groups 1
		set_gaia_object_only
		avoid_forest_zone 1
		terrain_to_place_on VAR_TERRAIN_HIGH
		avoid_forest_zone 3
		set_circular_placement
	}
	create_object DLC_ELEPHANT
	{
		set_place_for_every_player
		min_distance_to_players 15
		max_distance_to_players 20
		number_of_groups 1
		set_gaia_object_only
		avoid_forest_zone 1
		terrain_to_place_on VAR_TERRAIN_HIGH
		avoid_forest_zone 3
		set_circular_placement
	}
	
	/* Player Stragglers */
	create_object VAR_OBJECT_STRAGGLERTREE
	{
		number_of_groups 6
		set_place_for_every_player
		min_distance_to_players 2
		max_distance_to_players 8
		set_gaia_object_only
		terrain_to_place_on VAR_TERRAIN_HIGH
		temp_min_distance_group_placement 3
	}
endif